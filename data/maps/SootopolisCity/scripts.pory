const GOLD_ID = 11
const SILVER_ID = 12
const SHANE_ID = 13
const TYPLOSION_ID = 14
const FERALIGATR_ID = 15

mapscripts SootopolisCity_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		call_if_eq (VAR_LAZULAN_STATE, 3, SootopolisCity_EventScript_SetUpAfterThorne)
		call_if_eq (VAR_LAZULAN_STATE, 2, SootopolisCity_EventScript_SetUpAfterMtTempest)
	    call_if_eq (VAR_LAZULAN_STATE, 1, SootopolisCity_EventScript_SetUpAfterMegaStone)
        end
    }
}

script SootopolisCity_EventScript_SetUpAfterMegaStone {
	setobjectxyperm (FERALIGATR_ID, 25, 33)
	setobjectxyperm (SILVER_ID, 28, 32)
	setobjectmovementtype (SILVER_ID, MOVEMENT_TYPE_FACE_UP)
}

script SootopolisCity_EventScript_SetUpAfterMtTempest {
	setobjectxyperm (23, 36, 31)
	setobjectxyperm (24, 37, 31)
	setobjectmovementtype (23, MOVEMENT_TYPE_FACE_DOWN)
	setobjectmovementtype (24, MOVEMENT_TYPE_FACE_DOWN)
}

script SootopolisCity_EventScript_SetUpAfterThorne {
	setobjectxyperm (23, 30, 4)
	setobjectxyperm (24, 29, 4)
	setobjectmovementtype (23, MOVEMENT_TYPE_FACE_UP)
	setobjectmovementtype (24, MOVEMENT_TYPE_FACE_UP)
	setobjectxyperm (5, 33, 26)
	setobjectmovementtype (5, MOVEMENT_TYPE_FACE_DOWN)
	setobjectxyperm (7, 27, 38)
	setobjectmovementtype (7, MOVEMENT_TYPE_FACE_DOWN)
}

script SootopolisCity_EventScript_TriggerLeft {
	lock
	applymovement(OBJ_EVENT_ID_PLAYER, SootopolisCity_Movement_PlayerToEdgeLeft)
    waitmovement (0)
	goto (SootopolisCity_EventScript_GoldSilverMain)
}

script SootopolisCity_EventScript_TriggerRight {
	lock
	applymovement(OBJ_EVENT_ID_PLAYER, SootopolisCity_Movement_PlayerToEdgeRight)
    waitmovement (0)
	goto (SootopolisCity_EventScript_GoldSilverMain)
}

movement SootopolisCity_Movement_PlayerToEdgeLeft {
	walk_up * 2
	walk_left * 2
}

movement SootopolisCity_Movement_PlayerToEdgeRight {
	walk_up * 2
	walk_left * 3
}

script SootopolisCity_EventScript_GoldSilverMain {
	applymovement(SILVER_ID, SootopolisCity_Movement_StepRight)
	applymovement(SHANE_ID, SootopolisCity_Movement_StepRight)
    waitmovement (0)
	msgbox(format("DAD: {PLAYER}! Welcome to Lazulan City!\pCome over here, there's someone I would like you to meet."))
	waitmessage
	closemessage
	applymovement(SILVER_ID, SootopolisCity_Movement_SilverUpFaceDown)
    waitmovement (0)
	applymovement(OBJ_EVENT_ID_PLAYER, SootopolisCity_Movement_PlayerToAction)
    waitmovement (0)
	applymovement(SHANE_ID, SootopolisCity_Movement_StepUp)
    waitmovement (0)
	msgbox(format("SHANE: Took you long enough to get here."))
	waitmessage
	applymovement(SILVER_ID, SootopolisCity_Movement_StepLeft)
	applymovement(OBJ_EVENT_ID_PLAYER, SootopolisCity_Movement_StepLeft)
	applymovement(SHANE_ID, SootopolisCity_Movement_StepLeft)
    waitmovement (0)
	msgbox(format("DAD: This is my old friend, GOLD.\pHe and I have a long history together.\pWe've had our differences in the past, but that was long ago, and I wouldn't be the person I am today if not for him."))
	waitmessage
	applymovement(GOLD_ID, SootopolisCity_Movement_StepRight)
    waitmovement (0)
	msgbox(format("GOLD: Come on, SILVER, you give me too much credit.\pGlad to meet you, {PLAYER}.\pYour father's told me a lot about you two.\pSo, are you enjoying the Golden Islands so far?\p{PLAYER}: ...\p..."))
	waitmessage
	applymovement(GOLD_ID, SootopolisCity_Movement_StepRight)
    waitmovement (0)
	msgbox(format("GOLD: Not as talkative as your brother, huh?\pThat's ok, I get it.\pI was exactly the same when I was your age.\pI was so shy, I could barely speak to anyone.\pWell, no need to rush it, I'm sure you'll come out of your shell in time."))
	waitmessage
	applymovement(GOLD_ID, SootopolisCity_Movement_StepRight)
    waitmovement (0)
	msgbox(format("Now, about why you're all here.\pAre you familiar with the Millennium Comet?\pOnce every thousand years, the Millennium Comet can be seen in the sky for seven nights.\pThey're holding a festival here to celebrate!\pAlthough, the festival won't start for another few days."))
	waitmessage
	applymovement(SHANE_ID, SootopolisCity_Movement_StepLeft)
    waitmovement (0)
	msgbox(format("SHANE: Is it true that the comet has some kind of connection to the Legendary POKéMON, Jirachi?"))
	waitmessage
	applymovement(GOLD_ID, SootopolisCity_Movement_StepRight)
    waitmovement (0)
	msgbox(format("GOLD: Well, that is the rumor.\pAlthough, no one has seen Jirachi for centuries, so we can't say for sure."))
	waitmessage
	applymovement(SILVER_ID, SootopolisCity_Movement_StepDown)
    waitmovement (0)
	msgbox(format("DAD: If you two are lucky, maybe you'll encounter Jirachi while we're here."))
	waitmessage
	applymovement(SILVER_ID, SootopolisCity_Movement_StepLeft)
	applymovement(GOLD_ID, SootopolisCity_Movement_StepRight)
    waitmovement (0)
	msgbox(format("GOLD: Hah! From what you've told me, these two might actually be that lucky!\p...\pNow, SHANE and {PLAYER}, there's something I would like to show you.\pPlease, come with me."))
	waitmessage
	closemessage
	applymovement(GOLD_ID, SootopolisCity_Movement_WalkUpOne)
    waitmovement (0)
	applymovement(GOLD_ID, SootopolisCity_Movement_WalkUpOne)
    applymovement(TYPLOSION_ID, SootopolisCity_Movement_TyFollowGold)
    waitmovement (0)
	opendoor (45, 27)
	waitdooranim
	applymovement(GOLD_ID, SootopolisCity_Movement_WalkUpOneInvis)
    applymovement(TYPLOSION_ID, SootopolisCity_Movement_UpTwoInvis)
    waitmovement (0)
	closedoor (45, 27)
	waitdooranim
	removeobject (GOLD_ID)
	removeobject (TYPLOSION_ID)
	applymovement(OBJ_EVENT_ID_PLAYER, SootopolisCity_Movement_StepDown)
	applymovement(SHANE_ID, SootopolisCity_Movement_StepUp)
    waitmovement (0)
	msgbox(format("SHANE: If anyone's going to catch Jirachi, it's going to be me!"))
	waitmessage
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, SootopolisCity_Movement_StepLeft)
	applymovement(SHANE_ID, SootopolisCity_Movement_ShaneToDoor)
    waitmovement (0)
	opendoor (45, 27)
	waitdooranim
	applymovement(SHANE_ID, SootopolisCity_Movement_WalkUpOneInvis)
    waitmovement (0)
	closedoor (45, 27)
	waitdooranim
	removeobject (SHANE_ID)
	setflag (FLAG_HIDE_SHANE_INITIAL_LAZULAN)
	setflag (FLAG_VISITED_SOOTOPOLIS_CITY)
	applymovement(OBJ_EVENT_ID_PLAYER, SootopolisCity_Movement_StepUp)
	applymovement(SILVER_ID, SootopolisCity_Movement_StepDown)
    waitmovement (0)
	msgbox(format("DAD: Go on, {PLAYER}.\pI'm going to explore the town some more."))
	waitmessage
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, SootopolisCity_Movement_PlayerToDoor)
    waitmovement (0)
	warpdoor (MAP_SOOTOPOLIS_CITY_MYSTERY_EVENTS_HOUSE_1F, 0)
    waitstate
    release
    end
}

movement SootopolisCity_Movement_StepRight {
	walk_in_place_right
}

movement SootopolisCity_Movement_StepLeft {
	walk_in_place_left
}

movement SootopolisCity_Movement_StepUp {
	walk_in_place_up
}

movement SootopolisCity_Movement_StepDown {
	walk_in_place_down
}

movement SootopolisCity_Movement_SilverUpFaceDown {
	walk_up
	walk_in_place_down
}

movement SootopolisCity_Movement_PlayerToAction {
	walk_left * 3
	walk_in_place_down
}

movement SootopolisCity_Movement_WalkUpOne {
	walk_up
}

movement SootopolisCity_Movement_TyFollowGold {
	walk_right
	walk_in_place_up
}

movement SootopolisCity_Movement_WalkUpOneInvis {
	walk_up
	set_invisible
}

movement SootopolisCity_Movement_UpTwoInvis {
	walk_up * 2
	set_invisible
}

movement SootopolisCity_Movement_ShaneToDoor {
	walk_left * 2
	walk_up * 3
}

movement SootopolisCity_Movement_PlayerToDoor {
	walk_left * 2
	walk_up * 2
}

script SootopolisCity_EventScript_Kiri {
	lock
	faceplayer
	dotimebasedevents
	special (GetPlayerBigGuyGirlString)
	goto_if_set (FLAG_DAILY_SOOTOPOLIS_RECEIVED_BERRY, SootopolisCity_EventScript_KiriReceivedBerry)
	msgbox (SootopolisCity_Text_NameIsKiriHaveOneOfThese, MSGBOX_DEFAULT)
	random (NUM_KIRI_BERRIES)
	addvar (VAR_RESULT, NUM_KIRI_BERRIES_SKIPPED)
	addvar (VAR_RESULT, FIRST_BERRY_INDEX)
	giveitem (VAR_RESULT)
	goto_if_eq (VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
	setflag (FLAG_DAILY_SOOTOPOLIS_RECEIVED_BERRY)
	msgbox (SootopolisCity_Text_GiveYouThisBerryToo, MSGBOX_DEFAULT)
	random (2)
	goto_if_eq (VAR_RESULT, 0, SootopolisCity_EventScript_GiveFigyBerry)
	goto_if_eq (VAR_RESULT, 1, SootopolisCity_EventScript_GiveIapapaBerry)
	end
}

script SootopolisCity_EventScript_GiveFigyBerry {
	giveitem (ITEM_FIGY_BERRY)
	goto_if_eq (VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
	msgbox (SootopolisCity_Text_WhatKindOfWishInYourName, MSGBOX_DEFAULT)
	release
	end
}

script SootopolisCity_EventScript_GiveIapapaBerry {
	giveitem (ITEM_IAPAPA_BERRY)
	goto_if_eq (VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
	msgbox (SootopolisCity_Text_WhatKindOfWishInYourName, MSGBOX_DEFAULT)
	release
	end
}

script SootopolisCity_EventScript_KiriReceivedBerry {
	msgbox (SootopolisCity_Text_LikeSeasonBornIn, MSGBOX_YESNO)
	goto_if_eq (VAR_RESULT, YES, SootopolisCity_EventScript_KiriLikeSeasonBornIn)
	msgbox (SootopolisCity_Text_OhDoesntMatter, MSGBOX_DEFAULT)
	release
	end
}

script SootopolisCity_EventScript_KiriLikeSeasonBornIn {
	msgbox (SootopolisCity_Text_ThenILoveAutumn, MSGBOX_DEFAULT)
	release
	end
}

script SootopolisCity_EventScript_Woman2 {
    lock
	faceplayer
	if (flag(FLAG_HIDE_SOOTOPOLIS_CITY_RESIDENTS)) {
		msgbox(format("I can't wait for the upcoming festival!\pIt looks like they're setting up a market in the middle of town.\pI bet they'll sell all kinds of rare items!"))
		release
		end
	}
	else {
		msgbox(format("Have you stopped by the market in the middle of town yet?\pThey're selling all kinds of rare items you can't buy anywhere else!"))
		release
		end
	}
}

script SootopolisCity_EventScript_Man {
    lock
	faceplayer
	if (flag(FLAG_HIDE_SOOTOPOLIS_CITY_RESIDENTS)) {
		msgbox(format("Hey there! Are you looking forward to the festival?\pI'm still getting set up here, but I'll have some good TM's for sale later!"))
		release
		end
	}
	else {
		message (gText_HowMayIServeYou)
		waitmessage
		pokemart (SootopolisCity_Mart_Salesman2)
		msgbox (gText_PleaseComeAgain, MSGBOX_DEFAULT)
		release
		end
	}
}

mart SootopolisCity_Mart_Salesman2 {
	ITEM_TM26
	ITEM_TM27
	ITEM_TM28
	ITEM_TM29
	ITEM_TM30
	ITEM_TM31
	ITEM_TM32
	ITEM_TM33
	ITEM_TM34
	ITEM_TM35
	ITEM_TM36
	ITEM_TM37
	ITEM_TM38
	ITEM_TM39
	ITEM_TM40
	ITEM_TM41
	ITEM_TM42
	ITEM_TM43
	ITEM_TM44
	ITEM_TM45
	ITEM_TM46
	ITEM_TM47
	ITEM_TM48
	ITEM_TM49
	ITEM_TM50
}

script SootopolisCity_EventScript_Woman1 {
    msgbox(format("I heard a rumor that the Legendary POKéMON, Jirachi, can be seen high up in the mountains.\pHmm... We do have Mt. Tempest nearby!"), MSGBOX_NPC)
	end
}

script SootopolisCity_EventScript_NinjaBoy {
    msgbox(format("Oh! You have a Mega Stone!?\pThat's so cool!\pI wish my parents would buy me one!"), MSGBOX_NPC)
	end
}

script SootopolisCity_EventScript_Boy1 {
    msgbox(format("There's a guy in town who is crazy about Heart Scales.\pI don't really get it, but at least he'll trade Exp. Candy for them!"), MSGBOX_NPC)
	end
}

script SootopolisCity_EventScript_GymSign {
	msgbox (SootopolisCity_Text_GymSign, MSGBOX_SIGN)
	end
}

script SootopolisCity_EventScript_CitySign {
	msgbox (SootopolisCity_Text_CitySign, MSGBOX_SIGN)
	end
}

script SootopolisCity_EventScript_Boy2 {
    lock
	faceplayer
	if (flag(FLAG_HIDE_SOOTOPOLIS_CITY_RESIDENTS)) {
		msgbox(format("Man, there's already starting to be tourists around town.\pWhy does my hometown have to host this festival?"))
		release
		end
	}
	else {
		msgbox(format("Whoa, did you see the Kimono Girls dancing around town?\pI can't believe how pretty they are!"))
		release
		end
	}
}

script SootopolisCity_EventScript_BlackBelt {
    msgbox(format("Now this is the kind of town I just love!\pYou'll get no complaints from this man!"), MSGBOX_NPC)
	end
}

script SootopolisCity_EventScript_Girl {
    msgbox(format("Somebody needs to sweep all of this sand up, it keeps getting in my sandals, and I hate it!"), MSGBOX_NPC)
	end
}

script SootopolisCity_EventScript_Maniac {
    lock
	faceplayer
	if (flag(FLAG_HIDE_SOOTOPOLIS_CITY_RESIDENTS)) {
		msgbox(format("Sorry, you'll have to come back later.\pI'm still setting up shop."))
		release
		end
	}
	else {
		message (gText_HowMayIServeYou)
		waitmessage
		pokemart (SootopolisCity_Mart_Salesman5)
		msgbox (gText_PleaseComeAgain, MSGBOX_DEFAULT)
		release
		end
	}
}

mart SootopolisCity_Mart_Salesman5 {
	ITEM_HP_UP
	ITEM_PROTEIN
	ITEM_IRON
	ITEM_CALCIUM
	ITEM_ZINC
	ITEM_CARBOS
	ITEM_PP_UP
	ITEM_RARE_CANDY
}

script SootopolisCity_EventScript_Salesman1 {
	lock
	faceplayer
	message (gText_HowMayIServeYou)
	waitmessage
	pokemart (SootopolisCity_Mart_Salesman1)
	msgbox (gText_PleaseComeAgain, MSGBOX_DEFAULT)
	release
	end
}

mart SootopolisCity_Mart_Salesman1 {
	ITEM_TM01
	ITEM_TM02
	ITEM_TM03
	ITEM_TM04
	ITEM_TM05
	ITEM_TM06
	ITEM_TM07
	ITEM_TM08
	ITEM_TM09
	ITEM_TM10
	ITEM_TM11
	ITEM_TM12
	ITEM_TM13
	ITEM_TM14
	ITEM_TM15
	ITEM_TM16
	ITEM_TM17
	ITEM_TM18
	ITEM_TM19
	ITEM_TM20
	ITEM_TM21
	ITEM_TM22
	ITEM_TM23
	ITEM_TM24
	ITEM_TM25
}

script SootopolisCity_EventScript_Salesman3 {
	lock
	faceplayer
	message (gText_HowMayIServeYou)
	waitmessage
	pokemart (SootopolisCity_Mart_Salesman3)
	msgbox (gText_PleaseComeAgain, MSGBOX_DEFAULT)
	release
	end
}

mart SootopolisCity_Mart_Salesman3 {
	ITEM_FIRE_STONE
	ITEM_WATER_STONE
	ITEM_THUNDER_STONE
	ITEM_LEAF_STONE
	ITEM_SUN_STONE
	ITEM_MOON_STONE
	ITEM_SHINY_STONE
	ITEM_DUSK_STONE
	ITEM_DAWN_STONE
	ITEM_ICE_STONE
	ITEM_OVAL_STONE
	ITEM_BIKE_VOUCHER
}

script SootopolisCity_EventScript_Salesman4 {
	lock
	faceplayer
	message (gText_HowMayIServeYou)
	waitmessage
	pokemart (SootopolisCity_Mart_Salesman4)
	msgbox (gText_PleaseComeAgain, MSGBOX_DEFAULT)
	release
	end
}

mart SootopolisCity_Mart_Salesman4 {
	ITEM_SLOWBRONITE
	ITEM_KANGASKHANITE
	ITEM_AERODACTYLITE
	ITEM_AMPHAROSITE
	ITEM_HERACRONITE
	ITEM_HOUNDOOMINITE
	ITEM_SABLENITE
	ITEM_AGGRONITE
	ITEM_ABSOLITE
	ITEM_SALAMENCITE
	ITEM_GARCHOMPITE
	ITEM_LUCARIONITE
	ITEM_ABOMASITE
	ITEM_GALLADITE
	ITEM_AUDINITE
}

script SootopolisCity_EventScript_Salesman6 {
	lock
	faceplayer
	message (gText_HowMayIServeYou)
	waitmessage
	pokemart (SootopolisCity_Mart_Salesman6)
	msgbox (gText_PleaseComeAgain, MSGBOX_DEFAULT)
	release
	end
}

mart SootopolisCity_Mart_Salesman6 {
	ITEM_LIECHI_BERRY
	ITEM_GANLON_BERRY
	ITEM_SALAC_BERRY
	ITEM_PETAYA_BERRY
	ITEM_APICOT_BERRY
	ITEM_LANSAT_BERRY
	ITEM_STARF_BERRY
	ITEM_MICLE_BERRY
	ITEM_CUSTAP_BERRY
	ITEM_JABOCA_BERRY
	ITEM_ROWAP_BERRY
	ITEM_KEE_BERRY
	ITEM_MARANGA_BERRY
	ITEM_ENIGMA_BERRY
}

script SootopolisCity_EventScript_Typhlosion {
	lock
    faceplayer
	waitse
	playmoncry (SPECIES_TYPHLOSION, CRY_MODE_NORMAL)
    applymovement (TYPLOSION_ID, LittlerootTown_BrendansHouse_1F_Heart)
    msgbox (format("TYPHLOSION: Wrrahh!"))
    waitmovement (0)
    waitmoncry
    release
    end
}

script SootopolisCity_EventScript_Feraligatr {
	lock
    faceplayer
	waitse
	playmoncry (SPECIES_FERALIGATR, CRY_MODE_NORMAL)
    applymovement (FERALIGATR_ID, LittlerootTown_BrendansHouse_1F_Heart)
    msgbox (format("FERALIGATR: Grrahh!"))
    waitmovement (0)
    waitmoncry
    release
    end
}

script SootopolisCity_EventScript_Silver {
	msgbox(format("DAD: I see you're wearing a Mega Ring now, that's great!\pHave you had a chance to use it yet?\pI really feel like the connection I share with my POKéMON is stronger when I Mega Evolve them."), MSGBOX_NPC)
	end
}

script SootopolisCity_EventScript_SilverPostThorne {
	lock
	faceplayer
	if (flag(FLAG_HIDE_GOLD_LAZULAN)) {
		msgbox(format("DAD: Hey, {PLAYER}, what do you think of the festival?\pThey sure are selling a lot of good items that you can't find anywhere else."))
		release
		end
	}
	else {
		msgbox(format("DAD: Hey, {PLAYER}, what do you think of the festival?\pThey sure are selling a lot of good items that you can't find anywhere else.\pBy the way, did you talk to GOLD yet?\pHe was looking for you.\pI think he's on the north side of town now."))
		release
		end
	}
}

script SootopolisCity_EventScript_ShanePostThorne {
	msgbox(format("SHANE: Hey, this festival isn't half bad.\pI picked up a ton of new Mega Stones from one of the salesmen."), MSGBOX_NPC)
	end
}

script SootopolisCity_EventScript_Gold {
	lock
	faceplayer
	if (flag(FLAG_HIDE_SOOTOPOLIS_CITY_RESIDENTS)) {
		msgbox(format("GOLD: Yo, {PLAYER}!\pYour father told me about how you and SHANE defeated Team Phoenix, and how they were actually formed from the remnants of Team Rocket.\pYou know, I battled them many years ago myself.\pThey forced all of the Magikarp in the Lake of Rage to evolve for whatever reason.\pAh, I won't bore you with stories of the past though."))
		release
		end
	}
	else {
		msgbox(format("GOLD: There you are, {PLAYER}.\pYou know, from the moment I saw you, I've had this strange feeling nagging at me.\pI wasn't sure what it was, but as your father told me more about your exploits, it hit me.\pIn all of my years of battling, I've only lost to a single TRAINER.\pIt was someone who I'd never met before, and who I swear vanished before my eyes right after defeating me."))
		waitmessage
        checkplayergender
        if (var(VAR_RESULT) == MALE) {
            call (SootopolisCity_EventScript_MalePlayerGold)
        }
        elif (var(VAR_RESULT) == FEMALE) {
            call (SootopolisCity_EventScript_FemalePlayerGold)
        }
		msgbox(format("Now, I wouldn't want to disrupt the festival with our battle.\pSo, if you're feeling up to the challenge, then meet me at my private island on Route G.\pWe won't have to worry about collateral damage there.\pUntil next time, {PLAYER}."))
		waitmessage
		closemessage
		specialvar (VAR_RESULT, GetPlayerFacingDirection)
        if (var(VAR_RESULT) == DIR_NORTH) {
            applymovement(OBJ_EVENT_ID_PLAYER, SootopolisCity_Movement_PlayerMoveForGold)
    		waitmovement (0)
        }
		applymovement (23, SootopolisCity_Movement_GoldToHisHouse)
		applymovement (24, SootopolisCity_Movement_TyphlosionToGoldHouse)
		waitmovement (0)
		removeobject (23)
		removeobject (24)
		setflag (FLAG_HIDE_GOLD_LAZULAN)
		clearflag (FLAG_HIDE_GOLD_ROUTE_G)
		release
		end
	}
}

script SootopolisCity_EventScript_MalePlayerGold {
	msgbox(format("I know this will sound crazy, but you remind me of that mystery boy.\pI know I can't go back in time to challenge him to a rematch, but maybe battling with you will bring me some closure."))
}

script SootopolisCity_EventScript_FemalePlayerGold {
	msgbox(format("I know this will sound crazy, but you remind me of that mystery girl.\pI know I can't go back in time to challenge her to a rematch, but maybe battling with you will bring me some closure."))
}

movement SootopolisCity_Movement_PlayerMoveForGold {
	walk_left
	walk_in_place_right
}

movement SootopolisCity_Movement_GoldToHisHouse {
	walk_down
	walk_right
	walk_down * 2
	walk_right
	walk_down * 4
}

movement SootopolisCity_Movement_TyphlosionToGoldHouse {
	walk_right
	walk_down
	walk_right
	walk_down * 2
	walk_right
	walk_down * 4
}

script SootopolisCity_EventScript_KimonoGirls {
	msgbox(format("I hope you're enjoying the festival!"), MSGBOX_NPC)
	end
}

script SootopolisCity_EventScript_BaldMan {
	msgbox(format("They're selling BERRIES? Really?\pYou could just go pick those out of the ground if you know where to look."), MSGBOX_NPC)
	end
}

script SootopolisCity_EventScript_Beauty {
	msgbox(format("They actually sell Trade Stones here, I couldn't believe it!"), MSGBOX_NPC)
	end
}

script SootopolisCity_EventScript_Cowgirl {
	msgbox(format("This festival sure is somethin' else.\pI can't wait to tell everyone at the farm about it!"), MSGBOX_NPC)
	end
}

raw`
SootopolisCity_Text_GymSign:
	.string "MEGA EVOLUTION RESEARCH LAB$"

SootopolisCity_Text_CitySign:
	.string "LAZULAN CITY\n"
	.string "“The festive island retreat.”$"
`