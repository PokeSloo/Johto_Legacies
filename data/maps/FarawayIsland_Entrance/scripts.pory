const LOCALID_SAILOR = 1
const LOCALID_SS_TIDAL = 2
const CELEBI_ID = 3
const POKEBALL_ID = 4

raw`
FarawayIsland_Entrance_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, FarawayIsland_Entrance_OnTransition
	.byte 0

FarawayIsland_Entrance_OnTransition:
	setflag FLAG_ARRIVED_ON_FARAWAY_ISLAND
	end

FarawayIsland_Entrance_EventScript_SetCloudsWeather::
	setweather WEATHER_SUNNY_CLOUDS
	doweather
	end

FarawayIsland_Entrance_EventScript_ClearWeather::
	setweather WEATHER_NONE
	doweather
	end

FarawayIsland_Entrance_EventScript_Sign::
	msgbox FarawayIsland_Entrance_Text_Sign, MSGBOX_SIGN
	end
`

script FarawayIsland_Entrance_EventScript_Sailor {
    lock
    faceplayer
    if (var(VAR_HARBOR_DEPARTED_FROM) == 1) {
        msgbox(format("Would you like to go back to Olivine City now?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == NO) {
            msgbox(format("Let me know when you're ready to leave."))
            release
            end
        }
        else {
            call (FarawayIsland_Entrance_EventScript_Depart)
            warpsilent (MAP_FORTREE_CITY_DECORATION_SHOP, 8, 6)
            waitstate
            release
            end
        }
    }
    else {
        msgbox(format("Would you like to go back to Apricotta Beach now?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == NO) {
            msgbox(format("Let me know when you're ready to leave."))
            release
            end
        }
        else {
            call (FarawayIsland_Entrance_EventScript_Depart)
            warpsilent (MAP_LILYCOVE_CITY_HARBOR, 8, 11)
            waitstate
            release
            end
        }
    }
}

script FarawayIsland_Entrance_EventScript_Depart {
    msgbox(format("All aboard!"))
    waitmessage
    closemessage
    applymovement (1, FortreeCity_DecorationShop_Movement_SailorBoard)
    waitmovement (0)
    delay (30)
    specialvar (VAR_RESULT, GetPlayerFacingDirection)
        switch (var(VAR_RESULT)) {
            case DIR_EAST :
                call (BirthIsland_Exterior_EventScript_FacingEast)
            case DIR_WEST :
                call (BirthIsland_Exterior_EventScript_FacingWest)
            case DIR_SOUTH :
                call (BirthIsland_Exterior_EventScript_FacingSouth)
        }
    delay (60)
    playse (SE_RG_SS_ANNE_HORN)
    applymovement (2, FortreeCity_DecorationShop_Movement_ShipLeave)
    waitmovement (0)
    waitse
    return
}

script FarawayIsland_Entrance_EventScript_Shrine {
    lock
    if (flag(FLAG_COMPLETED_MEWTWO_EVENT)) {
        release
        end
    }
    specialvar (VAR_RESULT, ScriptGetLeadMonSpecies)
    if (var(VAR_RESULT) == SPECIES_CELEBI) {
        playse (SE_PIN)
        applymovement (OBJ_EVENT_ID_PLAYER, FarawayIsland_Entrance_Movement_Exclamation)
        waitmovement (0)
        waitse
        delay (30)
        msgbox(format("Celebi is reacting to the shrine!\pWill you let it out?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES) {
            goto (FarawayIsland_Entrance_EventScript_BeginTimeTravel)
        }
        else {
            release
            end
        }
    }
    else {
        release
        end
    }
}

script FarawayIsland_Entrance_EventScript_BeginTimeTravel {
    closemessage
    applymovement (OBJ_EVENT_ID_PLAYER, SootopolisCity_Movement_StepLeft)
    waitmovement (0)
    delay (30)
    addobject (POKEBALL_ID)
    delay (30)
    playse (SE_BALL_OPEN)
    removeobject (POKEBALL_ID)
    addobject (CELEBI_ID)
    waitse
    delay (30)
    waitse
    playmoncry (SPECIES_CELEBI, CRY_MODE_NORMAL)
    waitmoncry
    playse (SE_LEDGE)
    applymovement (CELEBI_ID, FarawayIsland_Entrance_Movement_CelebiJump)
    waitmovement (0)
    playse (SE_LEDGE)
    applymovement (CELEBI_ID, FarawayIsland_Entrance_Movement_CelebiJump)
    waitmovement (0)
    fadescreen (FADE_TO_WHITE)
    removeobject (CELEBI_ID)
    playbgm (MUS_RAYQUAZA_APPEARS, FALSE)
    checkplayergender
    if (var(VAR_RESULT) == MALE) {
        setvar (VAR_0x8004, MALE)
    }
    elif (var(VAR_RESULT) == FEMALE) {
        setvar (VAR_0x8004, FEMALE)
    }
    setwarp (MAP_SHOAL_CAVE_LOW_TIDE_LOWER_ROOM, 34, 21)
    special (TimeTravelBackClock)
	waitstate
    fadedefaultbgm
    release
    end
}

movement FarawayIsland_Entrance_Movement_Exclamation {
    emote_exclamation_mark
}

movement FarawayIsland_Entrance_Movement_CelebiJump {
    jump_in_place_right
}