const LOCALID_SILVER = 1
const LOCALID_FERALIGATR = 2
const LOCALID_RED = 3
const LOCALID_BALL = 4

raw`
EverGrandeCity_ChampionsRoom_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, EverGrandeCity_ChampionsRoom_OnTransition
	map_script MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE, EverGrandeCity_ChampionsRoom_OnWarp
	map_script MAP_SCRIPT_ON_FRAME_TABLE, EverGrandeCity_ChampionsRoom_OnFrame
	.byte 0

EverGrandeCity_ChampionsRoom_OnTransition:
	call Common_EventScript_SetupRivalGfxId
	end

EverGrandeCity_ChampionsRoom_OnWarp:
	map_script_2 VAR_TEMP_1, 0, EverGrandeCity_ChampionsRoom_EventScript_PlayerTurnNorth
	.2byte 0

EverGrandeCity_ChampionsRoom_EventScript_PlayerTurnNorth::
	turnobject OBJ_EVENT_ID_PLAYER, DIR_NORTH
	end

EverGrandeCity_ChampionsRoom_OnFrame:
	map_script_2 VAR_TEMP_1, 0, EverGrandeCity_ChampionsRoom_EventScript_EnterRoom
	.2byte 0

EverGrandeCity_ChampionsRoom_EventScript_EnterRoom::
	lockall
	applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp4
	waitmovement 0
	delay 40
	applymovement OBJ_EVENT_ID_PLAYER, EverGrandeCity_ChampionsRoom_Movement_PlayerApproachWallace
	waitmovement 0
	setvar VAR_TEMP_1, 1
	goto EverGrandeCity_ChampionsRoom_EventScript_Wallace
	releaseall
	end

EverGrandeCity_ChampionsRoom_Movement_PlayerApproachWallace:
	walk_up
	walk_up
	step_end
`

script EverGrandeCity_ChampionsRoom_EventScript_Wallace {
	msgbox (format("DAD: I was wondering when you would show up, {PLAYER}.\pAfter SHANE challenged me, I figured you would be right behind him.\p...\pYou know, I felt a great sense of pride when I arrived at the Rage Resort, and found that you and SHANE defeated Team Phoenix by working together.\pI know he can be difficult, but I believe he just needs someone to show him a better path forward, just as someone once did for me."))
	waitmessage
	playbgm (MUS_ENCOUNTER_CHAMPION, FALSE)
	msgbox(format("Now {PLAYER}, I do not intend to go easy on you.\pAs the POKéMON League Champion, I must give it my all, and as your father, I hope you do the same.\pPrepare yourself!"))
	setvar (VAR_MUGSHOT_ID, MUGSHOT_CHAMPION)
	trainerbattle_no_intro (TRAINER_WALLACE, EverGrandeCity_ChampionsRoom_Text_Defeat)
	goto (EverGrandeCity_ChampionsRoom_EventScript_Defeated)
	end
}

text EverGrandeCity_ChampionsRoom_Text_Defeat {
	format("DAD: Well done, {PLAYER}.\pI gave that battle everything I had, and you still beat me.\pThe deep bond you share with your POKéMON has pushed you to incredible heights.\pYou are truly worthy of the title “Champion”.")
}

script EverGrandeCity_ChampionsRoom_EventScript_Defeated {
	playse (SE_DOOR)
	setmetatile (6, 1, METATILE_EliteFour_OpenDoorChampion_Frame, FALSE)
	setmetatile (6, 2, METATILE_EliteFour_OpenDoorChampion_Opening, FALSE)
	special (DrawWholeMapView)
	delay (30)
	applymovement (LOCALID_SILVER, EverGrandeCity_ChampionsRoom_Movement_StepRight)
	applymovement (LOCALID_FERALIGATR, EverGrandeCity_ChampionsRoom_Movement_StepLeft)
	waitmovement (0)
	msgbox(format("DAD: Thanks old friend, you fought well."))
	waitmessage
	closemessage
	waitse
	playmoncry (SPECIES_FERALIGATR, CRY_MODE_NORMAL)
    waitmoncry
	playse (SE_BALL_TRADE)
	removeobject (LOCALID_FERALIGATR)
	addobject (LOCALID_BALL)
	waitse
	setflag (FLAG_HIDE_FERALIGATR_POKEMON_LEAGUE)
	delay (60)
	removeobject (LOCALID_BALL)
	delay (60)
	applymovement (LOCALID_SILVER, EverGrandeCity_ChampionsRoom_Movement_StepDown)
	waitmovement (0)
	msgbox(format("DAD: {PLAYER}, the people and POKéMON you've met along your journey have shaped you into the person you are today, but your journey is still far from over.\pYou're still young, you have the rest of your life ahead of you."))
	waitmessage
	checkplayergender
	call_if_eq (VAR_RESULT, MALE, EverGrandeCity_ChampionsRoom_EventScript_FutureMale)
	call_if_eq (VAR_RESULT, FEMALE, EverGrandeCity_ChampionsRoom_EventScript_FutureFemale)
	applymovement (LOCALID_SILVER, EverGrandeCity_ChampionsRoom_Movement_StepDown)
	waitmovement (0)
	msgbox(format("Now then, please come with me."))
	waitmessage
	closemessage
	applymovement (LOCALID_SILVER, EverGrandeCity_ChampionsRoom_Movement_WallaceExit)
	applymovement (OBJ_EVENT_ID_PLAYER, EverGrandeCity_ChampionsRoom_Movement_PlayerExit)
	waitmovement (0)
	clearflag (FLAG_HIDE_SILVER_IN_HOUSE)
	addvar (VAR_CHERRYGROVE_STATE, 1)
	warp (MAP_EVER_GRANDE_CITY_HALL_OF_FAME, 7, 16)
	waitstate
	releaseall
	end
}

script EverGrandeCity_ChampionsRoom_EventScript_FutureMale {
	msgbox(format("There's no telling what the future holds, and I can't wait to see the man you become."))
	return
}

script EverGrandeCity_ChampionsRoom_EventScript_FutureFemale {
	msgbox(format("There's no telling what the future holds, and I can't wait to see the woman you become."))
	return
}

movement EverGrandeCity_ChampionsRoom_Movement_StepRight {
	walk_in_place_right
}

movement EverGrandeCity_ChampionsRoom_Movement_StepLeft {
	walk_in_place_left
}

movement EverGrandeCity_ChampionsRoom_Movement_StepDown {
	walk_in_place_down
}

movement EverGrandeCity_ChampionsRoom_Movement_PlayerExit {
	walk_up * 3
	delay_16
	walk_up
	delay_8
	set_invisible
	step_end
}

movement EverGrandeCity_ChampionsRoom_Movement_WallaceExit {
	walk_up * 3
	delay_8
	set_invisible
	step_end
}