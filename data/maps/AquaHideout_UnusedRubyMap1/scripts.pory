const SHANE_ID = 1
const ARIANA_ID = 2
const DAMON_ID = 3

mapscripts AquaHideout_UnusedRubyMap1_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_ECRUTEAK_STATE, 6: AquaHideout_UnusedRubyMap1_OnFrame
    ]
}

script AquaHideout_UnusedRubyMap1_OnFrame {
    lock
    delay (30)
    playse (SE_PIN)
    applymovement (SHANE_ID, MagmaHideout_4F_Movement_Exclamation)
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_Exclamation)
	waitmovement (0)
    waitse
    delay (30)
    applymovement (SHANE_ID, AquaHideout_UnusedRubyMap1_Movement_ShaneLookAround)
    applymovement (OBJ_EVENT_ID_PLAYER, AquaHideout_UnusedRubyMap1_Movement_PlayerLookAround)
	waitmovement (0)
    msgbox(format("SHANE: What the hell just happened?\pHow did we get outside, and where are we?\pSome small town?"))
    waitmessage
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_StepRight)
	waitmovement (0)
    msgbox(format("...\p...\pSHANE: You say you've experienced something similar before?\pThis is all too weird..."))
    waitmessage
    closemessage
    delay (20)
    playse (SE_PIN)
    applymovement (SHANE_ID, MagmaHideout_4F_Movement_DamonExclamationDown)
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_DamonExclamationDown)
	waitmovement (0)
    waitse
    delay (30)
    playbgm (MUS_RG_ROCKET_HIDEOUT, TRUE)
    applymovement (ARIANA_ID, AquaHideout_UnusedRubyMap1_Movement_ArianaApproach)
    applymovement (DAMON_ID, AquaHideout_UnusedRubyMap1_Movement_DamonApproach)
	waitmovement (0)
    msgbox(format("ARIANA: Well, this is quite the surprise!\pI wasn't expecting the two of you to get drawn into the past with us.\pCelebi's doing, perhaps...?\pNo matter, you children need to be taught some manners anyway.\pWe'll take care of you and then RED!"))
    waitmessage
    applymovement (DAMON_ID, AquaHideout_UnusedRubyMap1_Movement_StepLeft)
	waitmovement (0)
    msgbox(format("DAMON: Keep your guard up, Ma'am.\pThese brats are tougher than they look."))
    applymovement (SHANE_ID, MagmaHideout_4F_Movement_StepDown)
    waitmovement (0)
    msgbox(format("SHANE: You're damn right, we are!"))
    waitmessage
    applymovement (DAMON_ID, MagmaHideout_4F_Movement_StepUp)
	waitmovement (0)
    msgbox(format("ARIANA: I don't care how tough they are!"))
    waitmessage
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_StepUp)
	waitmovement (0)
    msgbox(format("I've dreamed of this moment for years, and I'll be damned if a couple of children stop me this close to my goal!\pCome on, DAMON, let's show them the true meaning of fear!"))
    waitmessage
    goto (AquaHideout_UnusedRubyMap1_BattlePrompt)
}

script AquaHideout_UnusedRubyMap1_BattlePrompt {
    applymovement (SHANE_ID, AquaHideout_UnusedRubyMap1_Movement_StepLeft)
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_StepRight)
	waitmovement (0)
    msgbox(format("SHANE: Pick your party members, let's destroy these nerds!"))
    waitmessage
    closemessage
    goto (AquaHideout_UnusedRubyMap1_ChooseParty)
}

script AquaHideout_UnusedRubyMap1_ChooseParty {
    special (SavePlayerParty)
	fadescreen (FADE_TO_BLACK)
	special (ChooseHalfPartyForBattle)
	waitstate
    goto_if_ne (VAR_RESULT, 0, AquaHideout_UnusedRubyMap1_DoBattle)
	special (LoadPlayerParty)
	goto (AquaHideout_UnusedRubyMap1_BattlePrompt)
}

script AquaHideout_UnusedRubyMap1_DoBattle {
    applymovement (SHANE_ID, MagmaHideout_4F_Movement_StepDown)
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_StepDown)
    waitmovement (0)
    applymovement (ARIANA_ID, AquaHideout_UnusedRubyMap1_Movement_Up1)
    applymovement (DAMON_ID, AquaHideout_UnusedRubyMap1_Movement_Up1)
	waitmovement (0)
    multi_2_vs_2 (TRAINER_MAXIE_MOSSDEEP, AquaHideout_UnusedRubyMap1_Text_ArianaDefeat, TRAINER_TABITHA_MAGMA_HIDEOUT, AquaHideout_UnusedRubyMap1_Text_DamonDefeat, TRAINER_MAY_RUSTBORO_TREECKO, TRAINER_BACK_PIC_WALLY)
    specialvar (VAR_RESULT, GetBattleOutcome)
    compare (VAR_RESULT, B_OUTCOME_LOST)
    goto_if_eq (AquaHideout_UnusedRubyMap1_PlayerLostTheFight)
    delay (30)
    clearflag (FLAG_HIDE_SILVER_RAGE_HIDEOUT)
    addvar (VAR_ECRUTEAK_STATE, 1)
    fadescreenspeed (FADE_TO_WHITE, 8)
    playbgm (MUS_RAYQUAZA_APPEARS, FALSE)
    setwarp (MAP_MAGMA_HIDEOUT_4F, 9, 8)
    special (TimeTravelForwardClock)
	waitstate
    fadedefaultbgm
    release
    end
}

script AquaHideout_UnusedRubyMap1_PlayerLostTheFight {
    fadescreen (FADE_TO_BLACK)
	special (SetCB2WhiteOut)
	waitstate
    release
    end
}

movement AquaHideout_UnusedRubyMap1_Movement_ShaneLookAround {
    walk_in_place_right
    delay_16 * 2
    walk_in_place_left
}

movement AquaHideout_UnusedRubyMap1_Movement_PlayerLookAround {
    walk_in_place_left
    delay_16 * 2
    walk_in_place_right
    delay_16 * 2
    walk_right
}

movement AquaHideout_UnusedRubyMap1_Movement_ArianaApproach {
    walk_up * 4
    walk_left * 4
    walk_in_place_up
}

movement AquaHideout_UnusedRubyMap1_Movement_DamonApproach {
    walk_up * 5
    walk_left * 3
    walk_in_place_up
}

movement AquaHideout_UnusedRubyMap1_Movement_StepLeft {
    walk_in_place_left
}

movement AquaHideout_UnusedRubyMap1_Movement_Up1 {
    walk_up
}

text AquaHideout_UnusedRubyMap1_Text_ArianaDefeat {
    format("ARIANA: No... This can't be...\pGIOVANNI...")
}

text AquaHideout_UnusedRubyMap1_Text_DamonDefeat {
    format("DAMON: This isn't over yet, you brats will pay for this insolence!")
}