const MOM_ID = 1
const SHANE_POST_E4_ID = 2
const SILVER_POST_E4_ID = 3
const SHANE_START_ID = 4
const ENTEI_EGG_ID = 5
const AZUMARILL_ID = 6
const FERALIGATR_ID = 7

mapscripts LittlerootTown_BrendansHouse_1F_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_CHERRYGROVE_STATE, 11: LittlerootTown_BrendansHouse_1F_EventScript_OnFrameAfterE4
    ]
}

script LittlerootTown_BrendansHouse_1F_EventScript_OnFrameAfterE4 {
    lock
    applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_FaceLeft)
    waitmovement (0)
    msgbox(format("MOM: There's my new Champion!\pYou're finally awake."))
    waitmessage
    closemessage
    applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_Movement_PlayerToTable)
    waitmovement (0)
    applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_FaceRight)
    waitmovement (0)
    delay(30)
    applymovement (SILVER_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_StepLeft)
    waitmovement (0)
    msgbox(format("DAD: Our battle must have exhausted you, {PLAYER}.\pYou've been sleeping all morning!"))
    waitmessage
    applymovement (SHANE_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_StepLeft)
    waitmovement (0)
    msgbox(format("SHANE: Yeah! It's about time you woke up, you lazy bum!"))
    waitmessage
    applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_Movement_StepRight)
    waitmovement (0)
    checkplayergender
    if (var(VAR_RESULT) == MALE) {
        call (LittlerootTown_BrendansHouse_1F_MalePlayerPostE4)
    }
    elif (var(VAR_RESULT) == FEMALE) {
        call (LittlerootTown_BrendansHouse_1F_FemalePlayerPostE4)
    }
    applymovement (SILVER_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_StepLeft)
    waitmovement (0)
    msgbox(format("DAD: Thanks LYRA, I'll take it from here."))
    waitmessage
    applymovement (SILVER_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_StepDown)
    applymovement (SHANE_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_StepUp)
    waitmovement (0)
    checkplayergender
    if (var(VAR_RESULT) == MALE) {
        call (LittlerootTown_BrendansHouse_1F_MalePlayerPostE4Dad)
    }
    elif (var(VAR_RESULT) == FEMALE) {
        call (LittlerootTown_BrendansHouse_1F_FemalePlayerPostE4Dad)
    }
    msgbox(format("We must strive to help all of those around us reach their full potential.\pBesides, you shouldn't discount your own growth.\pYou really had me on the ropes through most of our battle!"))
    waitmessage
    applymovement (SHANE_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_StepLeft)
    waitmovement (0)
    msgbox(format("SHANE: Well, whatever.\p...\pGood job, I guess... {PLAYER}."))
    waitmessage
    applymovement (SILVER_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_StepLeft)
    waitmovement (0)
    msgbox(format("DAD: Haha, it's a start.\p...\pYou know, I always thought I could balance my POKéMON League duties with my home life, but I was wrong.\pSeeing you kids again made me realize that I want to spend as much time with you all as possible.\pSo, I've decided to retire from the POKéMON League!"))
    waitmessage
    playse(SE_PIN)
    applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_Movement_Exclamation)
    applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_Movement_Exclamation)
    applymovement (SHANE_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_Exclamation)
    waitmovement (0)
    waitse
    delay(60)
    applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_Movement_StepRight)
    waitmovement (0)
    msgbox(format("MOM: That's wonderful!\pAlthough, it must have been a difficult decision.\pI know how much the League means to you."))
    applymovement (SILVER_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_StepLeft)
    waitmovement (0)
    msgbox(format("DAD: It was actually an easy choice to make.\p...\pBy the way, an old friend of mine has invited me to the Millennium Comet Festival in Lazulan City."))
    waitmessage
    applymovement (SILVER_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_StepDown)
    waitmovement (0)
    msgbox(format("{PLAYER}, SHANE, I would like to bring you two along with me."))
    waitmessage
    applymovement (SHANE_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_StepUp)
    waitmovement (0)
    msgbox(format("SHANE: Really!?\pI've always wanted to visit the Golden Islands!\pI hear there are all kinds of strong TRAINERS that gather there.\pDAD: True, but you two shouldn't have any issues with them.\pHere, you'll need this S.S. Ticket.\pHand it to the captian in Olivine City, and he'll take you to Apricotta Beach, a neighboring island."))
    waitmessage
    applymovement (SHANE_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_StepUp)
    waitmovement (0)
    msgbox(format("SHANE: This is great!\pI'll head there right now!"))
    waitmessage
    closemessage
    applymovement (SHANE_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_ShaneLeavePostE4)
    waitmovement (0)
    playse(SE_EXIT)
    removeobject (SHANE_POST_E4_ID)
    waitse
    applymovement (SILVER_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_SilverToPlayer)
    waitmovement (0)
    applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_Movement_StepDown)
    waitmovement (0)
    msgbox(format("DAD: Here, {PLAYER}."))
    waitmessage
    giveitem (ITEM_SS_TICKET)
    msgbox(format("DAD: I'll see you in Lazulan City.\pOh, by the way, I have a special gift for you.\pI'll leave it over here in case you need to make room in your party."))
    waitmessage
    closemessage
    applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_FaceLeft)
    applymovement (SILVER_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_SilverToCounter)
    waitmovement (0)
    delay (30)
    addobject (ENTEI_EGG_ID)
    clearflag (FLAG_HIDE_ENTEI_EGG)
    delay (60)
    applymovement (SILVER_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_SilverToGatr)
    waitmovement (0)
    applymovement (FERALIGATR_ID, LittlerootTown_BrendansHouse_1F_Movement_StepRight)
    waitmovement (0)
    msgbox(format("DAD: Until next time."))
    waitmessage
    closemessage
    applymovement (SILVER_POST_E4_ID, LittlerootTown_BrendansHouse_1F_Movement_SilverExit)
    applymovement (FERALIGATR_ID, LittlerootTown_BrendansHouse_1F_Movement_GatrExit)
    waitmovement (0)
    playse (SE_EXIT)
    waitse
    removeobject(SILVER_POST_E4_ID)
    removeobject(FERALIGATR_ID)
    setflag (FLAG_HIDE_SILVER_IN_HOUSE)
    setflag (FLAG_HIDE_GOLD_ROUTE_G)
    setflag (FLAG_HIDE_SILVER_OUTSIDE_ROUJEM)
    setflag (FLAG_HIDE_SHANE_LAZULAN_POST_THORNE)
    addvar (VAR_CHERRYGROVE_STATE, 1)
    release
    end
}

script LittlerootTown_BrendansHouse_1F_MalePlayerPostE4 {
    msgbox(format("MOM: SHANE!\pBe nice to your little brother!\pHe deserves a break."))
    waitmessage
    return
}

script LittlerootTown_BrendansHouse_1F_FemalePlayerPostE4 {
    msgbox(format("MOM: SHANE!\pBe nice to your little sister!\pShe deserves a break."))
    waitmessage
    return
}

script LittlerootTown_BrendansHouse_1F_MalePlayerPostE4Dad {
    msgbox(format("Look, SHANE, I can tell you feel like {PLAYER} has surpassed you, and I'm sure it seems unfair, but you shouldn't lash out at him for it.\pA truly confident man would be happy to see his little brother's growth, and be proud of him."))
    waitmessage
    return
}

script LittlerootTown_BrendansHouse_1F_FemalePlayerPostE4Dad {
    msgbox(format("Look, SHANE, I can tell you feel like {PLAYER} has surpassed you, and I'm sure it seems unfair, but you shouldn't lash out at her for it.\pA truly confident man would be happy to see his little sister's growth, and be proud of her."))
    waitmessage
    return
}

movement LittlerootTown_BrendansHouse_1F_Movement_PlayerToTable {
    walk_down
    walk_right
    walk_down * 3
    walk_right * 2
}

movement LittlerootTown_BrendansHouse_1F_Movement_StepLeft {
    walk_in_place_left
}

movement LittlerootTown_BrendansHouse_1F_Movement_StepRight {
    walk_in_place_right
}

movement LittlerootTown_BrendansHouse_1F_Movement_StepDown {
    walk_in_place_down
}

movement LittlerootTown_BrendansHouse_1F_Movement_StepUp {
    walk_in_place_up
}

movement LittlerootTown_BrendansHouse_1F_Movement_Exclamation {
    emote_exclamation_mark
}

movement LittlerootTown_BrendansHouse_1F_Movement_ShaneLeavePostE4 {
    walk_down
    walk_left * 6
    walk_in_place_down
}

movement LittlerootTown_BrendansHouse_1F_Movement_SilverToPlayer {
    walk_down * 2
    walk_left * 3
    walk_in_place_up
}

movement LittlerootTown_BrendansHouse_1F_Movement_SilverToCounter {
    walk_left
    walk_up * 4
    walk_in_place_right
}

movement LittlerootTown_BrendansHouse_1F_Movement_SilverToGatr {
    walk_left * 2
    walk_down * 2
    walk_in_place_right
}

movement LittlerootTown_BrendansHouse_1F_Movement_SilverExit {
    walk_down * 2
    set_invisible
}

movement LittlerootTown_BrendansHouse_1F_Movement_GatrExit {
    walk_right
    walk_down * 3
    set_invisible
}

script LittlerootTown_BrendansHouse_1F_EnteiEgg {
    lock
    getpartysize
    if (var(VAR_RESULT) == PARTY_SIZE) {
        msgbox (format("It's a POKéMON egg.\pThere's no more room in your party for it."))
        release
        end
    }
    else {
	    playfanfare (MUS_OBTAIN_ITEM)
	    msgbox (format("{PLAYER} received a POKéMON egg!"))
	    waitfanfare
	    giveegg (SPECIES_ENTEI)
        removeobject (ENTEI_EGG_ID)
        setflag (FLAG_HIDE_ENTEI_EGG)
        release
        end
    }
}

script LittlerootTown_BrendansHouse_1F_Trigger_1 {
    lock
    applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_FaceDown)
    applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_FaceLeft)
    waitmovement (0)
    msgbox (Come_Here)
    waitmessage
    closemessage
    applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_WalkDownFromLeft)
    waitmovement (0)
    goto (LittlerootTown_BrendansHouse_1F_Get_Started)
}

script LittlerootTown_BrendansHouse_1F_Trigger_2 {
    lock
    applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_FaceDown)
    applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_FaceLeft)
    waitmovement (0)
    msgbox (Come_Here)
    waitmessage
    closemessage
    applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_WalkDownFromMiddle)
    waitmovement (0)
    goto (LittlerootTown_BrendansHouse_1F_Get_Started)
}

script LittlerootTown_BrendansHouse_1F_Trigger_3 {
lock
applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_FaceDown)
applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_FaceLeft)
waitmovement (0)
msgbox (Come_Here)
waitmessage
closemessage
applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_WalkDownFromRight)
waitmovement (0)
goto (LittlerootTown_BrendansHouse_1F_Get_Started)
}

script LittlerootTown_BrendansHouse_1F_Get_Started {
msgbox (format("MOM: Today is the day!\pToday you get to go choose your very first POKéMON.\pI'm so excited for you!\pNow, Route 29 can be dangerous.\pThere are patches of grass where wild POKéMON can jump out and attack you.\pYou will need someone to go with you and keep you safe."))
waitmessage
closemessage
delay (10)
applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_FaceUp)
applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_FaceUp)
waitmovement (0)
delay (10)
msgbox ("SHANE!")
waitmessage
closemessage
delay (120)
clearflag (FLAG_GENERAL_HIDE)
playse (SE_EXIT)
addobject (SHANE_START_ID)
playbgm (MUS_HG_ENCOUNTER_RIVAL, FALSE)
applymovement (SHANE_START_ID, LittlerootTown_BrendansHouse_1F_StepDown1)
waitmovement (0)
msgbox (format("SHANE: What, MOM?\nI'm busy right now.\pMOM: You can't be that busy, we all hear your drumming up there.\pNow come over here, I need to talk to you.\pSHANE: Ugh, fine."))
waitmessage
closemessage
applymovement (SHANE_START_ID, LittlerootTown_BrendansHouse_1F_WalktoPlayer)
waitmovement (0)
applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_FaceLeft)
applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_PlayerStepDownFaceUP)
waitmovement (0)
checkplayergender
if (var(VAR_RESULT) == MALE) {
    call (LittlerootTown_BrendansHouse_1F_MalePlayer)
}
elif (var(VAR_RESULT) == FEMALE) {
    call (LittlerootTown_BrendansHouse_1F_FemalePlayer)
}
applymovement (SHANE_START_ID, LittlerootTown_BrendansHouse_1F_FaceDown)
waitmovement (0)
msgbox (format("Just keep quiet, and follow me."))
waitmessage
closemessage
applymovement (SHANE_START_ID, LittlerootTown_BrendansHouse_1F_ShaneLeave)
waitmovement (0)
playse (SE_EXIT)
removeobject (SHANE_START_ID)
setflag (FLAG_GENERAL_HIDE)
fadedefaultbgm
applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_StepUpFaceRight)
waitmovement (0)
msgbox (format("MOM: Don't worry, sweetie.\pIf SHANE gives you any trouble, just let me know.\pAzumarill and I will set him straight!\pOh, and don't forget to take your shoes with you."))
waitmessage
playfanfare (MUS_OBTAIN_ITEM)
msgbox (format("{PLAYER} received the running shoes!"))
waitfanfare
setflag (FLAG_SYS_B_DASH)
setflag (FLAG_RECEIVED_RUNNING_SHOES)
addvar (VAR_CHERRYGROVE_STATE, 1)
msgbox (format("Just remember to hold B when you want to run.\pNow go have fun!"))
waitmessage
closemessage
applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_FaceRight)
waitmovement (0)
release
end
}

script LittlerootTown_BrendansHouse_1F_MalePlayer {
    msgbox (format("MOM: SHANE, I need you to take your little brother over to New Bark Town to pick out his first POKéMON."))
    waitmessage
    applymovement (SHANE_START_ID, LittlerootTown_BrendansHouse_1F_StompRight)
    waitmovement (0)
    msgbox (format("SHANE: What!? Why do I have to?\pMOM: That's enough, SHANE.\pYou already have your first badge, and you're so close to getting your second.\p{PLAYER} can learn a lot from you.\pSHANE: Fine, I'll go."))
    waitmessage
    return
}

script LittlerootTown_BrendansHouse_1F_FemalePlayer {
    msgbox (format("MOM: SHANE, I need you to take your little sister over to New Bark Town to pick out her first POKéMON."))
    waitmessage
    applymovement (SHANE_START_ID, LittlerootTown_BrendansHouse_1F_StompRight)
    waitmovement (0)
    msgbox (format("SHANE: What!? Why do I have to?\pMOM: That's enough, SHANE.\pYou already have your first badge, and you're so close to getting your second.\p{PLAYER} can learn a lot from you.\pSHANE: Fine, I'll go."))
    waitmessage
    return
}

script LittlerootTown_BrendansHouse_1F_Azumarill {
    lock
    faceplayer
	waitse
	playmoncry (SPECIES_AZUMARILL, CRY_MODE_NORMAL)
    applymovement (AZUMARILL_ID, LittlerootTown_BrendansHouse_1F_Heart)
    msgbox (format("AZUMARILL: Azu! Azu!"))
    waitmovement (0)
    waitmoncry
    release
    end
}

script LittlerootTown_BrendansHouse_1F_PostBattleTrigger_1 {
lock
applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_FaceDown)
applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_FaceLeft)
waitmovement (0)
msgbox (LittlerootTown_BrendansHouse_1F_WhatHappened)
waitmessage
closemessage
applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_WalkDownFromLeft)
waitmovement (0)
goto (LittlerootTown_BrendansHouse_1F_Get_Nav)
}

script LittlerootTown_BrendansHouse_1F_PostBattleTrigger_2 {
lock
applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_FaceDown)
applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_FaceLeft)
waitmovement (0)
msgbox (LittlerootTown_BrendansHouse_1F_WhatHappened)
waitmessage
closemessage
applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_WalkDownFromMiddle)
waitmovement (0)
goto (LittlerootTown_BrendansHouse_1F_Get_Nav)
}

script LittlerootTown_BrendansHouse_1F_PostBattleTrigger_3 {
lock
applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_FaceDown)
applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_FaceLeft)
waitmovement (0)
msgbox (LittlerootTown_BrendansHouse_1F_WhatHappened)
waitmessage
closemessage
applymovement (OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_1F_WalkDownFromRight)
waitmovement (0)
goto (LittlerootTown_BrendansHouse_1F_Get_Nav)
}

script LittlerootTown_BrendansHouse_1F_Get_Nav {
    msgbox (format("...\p...\pUh huh...\p...\pOh that SHANE!\pHe's really on my last nerve now!\pOk, sweetie, take this with you."))
    waitmessage
    playfanfare (MUS_OBTAIN_ITEM)
	msgbox ("{PLAYER} received a POKéNAV.")
	waitfanfare
	setflag (FLAG_SYS_POKENAV_GET)
	setflag (FLAG_RECEIVED_POKENAV)
	setflag (FLAG_HIDE_SHANE_CHERRYGROVE_BATTLE)
    addvar (VAR_CHERRYGROVE_STATE, 1)
    msgbox (format("MOM: That is a POKéNAV, you can register people and call them anytime.\pIt also has a map of the Johto region.\pDon't hesitate to call me if SHANE acts up again.\pAzumarill and I will take care of him."))
    waitmessage
    applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_FaceRight)
    waitmovement (0)
    msgbox (format("Right, Azumarill?"))
    waitmessage
    waitse
	playmoncry (SPECIES_AZUMARILL, CRY_MODE_NORMAL)
    applymovement (AZUMARILL_ID, LittlerootTown_BrendansHouse_1F_DownExclamation)
    waitmovement (0)
    msgbox (format("AZUMARILL: Azu! Azu!"))
    waitmessage
    closemessage
    waitmoncry
    applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_FaceLeft)
    waitmovement (0)
    msgbox (format("MOM: Now get going, and don't worry about SHANE, ok."))
    waitmessage
    closemessage
    applymovement (MOM_ID, LittlerootTown_BrendansHouse_1F_FaceRight)
    waitmovement (0)
    setflag (FLAG_HAS_MATCH_CALL)
    setflag (FLAG_ADDED_MATCH_CALL_TO_POKENAV)
    setflag (FLAG_ENABLE_MOM_MATCH_CALL)
    setflag (FLAG_REGISTERED_STEVEN_POKENAV)
    setflag (FLAG_ADVENTURE_STARTED)
    release
    end
}

text Come_Here {
    format ("MOM: There you are, {PLAYER}.")
}

text LittlerootTown_BrendansHouse_1F_WhatHappened {
    format("MOM: {PLAYER}, you're awake!\pCome over and tell me what happened.")
}

movement LittlerootTown_BrendansHouse_1F_FaceDown {
    face_down
}

movement LittlerootTown_BrendansHouse_1F_FaceLeft {
    face_left
}

movement LittlerootTown_BrendansHouse_1F_FaceRight {
    face_right
}

movement LittlerootTown_BrendansHouse_1F_StepUpFaceRight {
    walk_up
    face_right
}

movement LittlerootTown_BrendansHouse_1F_WalkDownFromLeft {
    walk_down * 3
    walk_right * 3
}

movement LittlerootTown_BrendansHouse_1F_WalkDownFromMiddle {
    walk_down * 2
    walk_right * 2
}

movement LittlerootTown_BrendansHouse_1F_WalkDownFromRight {
    walk_down * 3
    walk_right
}

movement LittlerootTown_BrendansHouse_1F_FaceUp {
    face_up
}

movement LittlerootTown_BrendansHouse_1F_StepDown1 {
    walk_down
}

movement LittlerootTown_BrendansHouse_1F_WalktoPlayer {
    walk_down * 3
    walk_right
}

movement LittlerootTown_BrendansHouse_1F_ShaneLeave {
    walk_left
    walk_down * 2
}

movement LittlerootTown_BrendansHouse_1F_PlayerStepDownFaceUP {
    walk_down
    face_up
}

movement LittlerootTown_BrendansHouse_1F_StompRight {
    walk_in_place_right
}

movement LittlerootTown_BrendansHouse_1F_Heart {
    emote_heart
}

movement LittlerootTown_BrendansHouse_1F_DownExclamation {
    emote_exclamation_mark
    walk_in_place_down
    face_down
}

script LittlerootTown_BrendansHouse_1F_EventScript_Feraligatr {
	lock
    faceplayer
	waitse
	playmoncry (SPECIES_FERALIGATR, CRY_MODE_NORMAL)
    applymovement (FERALIGATR_ID, LittlerootTown_BrendansHouse_1F_Heart)
    msgbox (format("FERALIGATR: Grrahh!"))
    waitmovement (0)
    waitmoncry
    release
    end
}

script LittlerootTown_BrendansHouse_1F_EventScript_FinalSilver {
    msgbox(format("DAD: That festival was fun, but it's nice to be back home.\pI missed these relaxing days."), MSGBOX_NPC)
    end
}

script LittlerootTown_BrendansHouse_1F_EventScript_FinalShane {
    msgbox(format("SHANE: What are you looking at, nerd?\pI could totally beat the new League Champion too, just wait for me to train up a bit more!"), MSGBOX_NPC)
    end
}

script LittlerootTown_BrendansHouse_1F_EventScript_Mom {
    lock
    faceplayer
    if(flag(FLAG_DEFEATED_RED)) {
        msgbox(format("MOM: It's so nice to have your father home.\pYou know, we really didn't get along very well when we first met.\pFate sure has a strange sense of humor."))
        release
        end
    }
    elif(flag(FLAG_IS_CHAMPION)) {
        msgbox(format("MOM: It was so nice to see your father again.\pIt might be greedy of me, but I hope he's all finished with battling now.\pThen he could just stay here all the time!"))
        release
        end
    }
    elif(flag(FLAG_HOOH_VS_LUGIA_COMPLETED)) {
        msgbox(format("MOM: Your father called to tell me all about what happened with Lugia and Ho-oh.\pWe're both so proud of you!\pOh, and I heard you met your grandmother...\pI hope you can forgive us for hiding her from you.\pShe's such a nasty woman."))
        release
        end
    }
    elif(flag(FLAG_DEFEATED_DAMON_KIZU)) {
        msgbox(format("...\pMOM: What's that sweetie?\p...\pHmm... Battling criminal groups and saving POKéMON is right up your father's alley.\pSomeone needs to stop those jerks!"))
        release
        end
    }
    elif(flag(FLAG_BADGE05_GET)) {
        msgbox(format("MOM: You saw a live Thunderbolts concert?\pThat sounds like fun!\pI hope SHANE got to watch them too.\pThe Thunderbolts are his favorite band, you know!"))
        release
        end
    }
    elif(flag(FLAG_HIDE_SHANE_PHOENIX_CELL)) {
        msgbox(format("...\pMOM: What's that sweetie?\p...\pThis Phoenix gang sounds like a nasty bunch of folks!\pWe used to have a similar group who called themselves Team Rocket."))
        release
        end
    }
    elif(flag(FLAG_ILEX_BLOCKING_GRUNT)) {
        msgbox(format("...\pMOM: What's that sweetie?\p...\pTime travel?\pYou kids sure have wild imaginations!"))
        release
        end
    }
    elif(flag(FLAG_BADGE02_GET)) {
        msgbox(format("MOM: Oh, you have two badges now?\pKeep at it, {PLAYER}!\pI know you'll become a POKéMON Master one day."))
        release
        end
    }
    elif(flag(FLAG_HIDE_RT31_RAIKOU)) {
        msgbox(format("...\pMOM: What's that sweetie?\pYou saw a legendary POKéMON?\pYellow with black stripes?\pYour father will love to hear about this."))
        release
        end
    }
    elif(flag(FLAG_HAS_MATCH_CALL)) {
        msgbox(format("MOM: That POKéNAV is a really handy tool.\pAll of the best POKéMON TRAINERS have one!"))
        release
        end
    }
    else {
        msgbox(format("MOM: Don't worry, sweetie.\pIf SHANE gives you any trouble, just let me know.\pAzumarill and I will set him straight!"))
        release
        end
    }
}