const SHANE_ID = 1
const ARIANA_ID = 6
const DAMON_ID = 5
const SILVER_ID = 11
const FERALIGATR_ID = 12
const SCIENTIST_1_ID = 10
const SCIENTIST_2_ID = 9
const CELEBI_ID = 7

mapscripts MagmaHideout_4F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call_if_eq (VAR_ECRUTEAK_STATE, 7, MagmaHideout_4F_EventScript_SetUpAfterPallet)
        call_if_eq (VAR_ECRUTEAK_STATE, 8, MagmaHideout_4F_EventScript_SetUpAfterEcruteak)
        end
    }
    MAP_SCRIPT_ON_LOAD {
        if (flag(FLAG_CELEBI_TANK_BROKEN)) {
            setmetatile (8, 5, 0x230, TRUE)
            setmetatile (9, 5, 0x231, TRUE)
            setmetatile (10, 5, 0x232, TRUE)
            setmetatile (8, 4, 0x228, TRUE)
            setmetatile (9, 4, 0x229, TRUE)
            setmetatile (10, 4, 0x22A, TRUE)
            setmetatile (8, 3, 0x220, TRUE)
            setmetatile (9, 3, 0x221, TRUE)
            setmetatile (10, 3, 0x222, TRUE)
            end
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_ECRUTEAK_STATE, 7: MagmaHideout_4F_EventScript_OnFrameAfterPallet
        VAR_ECRUTEAK_STATE, 8: MagmaHideout_4F_EventScript_OnFrameAfterEcruteak
    ]
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_ECRUTEAK_STATE, 7: MagmaHideout_4F_EventScript_OnWarp
        VAR_ECRUTEAK_STATE, 8: MagmaHideout_4F_EventScript_OnWarp
    ]
}

script MagmaHideout_4F_EventScript_SetUpAfterPallet {
    setobjectxyperm (SHANE_ID, 8, 8)
	setobjectmovementtype (SHANE_ID, MOVEMENT_TYPE_FACE_UP)
    setobjectmovementtype (ARIANA_ID, MOVEMENT_TYPE_FACE_DOWN)
    setobjectmovementtype (DAMON_ID, MOVEMENT_TYPE_FACE_DOWN)
    setobjectmovementtype (SCIENTIST_1_ID, MOVEMENT_TYPE_FACE_DOWN)
	return
}

script MagmaHideout_4F_EventScript_SetUpAfterEcruteak {
    setobjectxyperm (SHANE_ID, 7, 9)
    setobjectxyperm (DAMON_ID, 7, 6)
    setobjectxyperm (SILVER_ID, 9, 8)
    setobjectxyperm (FERALIGATR_ID, 8, 8)
	setobjectmovementtype (SHANE_ID, MOVEMENT_TYPE_FACE_UP)
    setobjectmovementtype (ARIANA_ID, MOVEMENT_TYPE_FACE_DOWN)
    setobjectmovementtype (DAMON_ID, MOVEMENT_TYPE_FACE_DOWN)
    setobjectmovementtype (SCIENTIST_1_ID, MOVEMENT_TYPE_FACE_DOWN)
    setobjectmovementtype (SCIENTIST_2_ID, MOVEMENT_TYPE_FACE_DOWN)
    setobjectmovementtype (SILVER_ID, MOVEMENT_TYPE_FACE_UP)
    setobjectmovementtype (FERALIGATR_ID, MOVEMENT_TYPE_FACE_UP)
	return
}

script MagmaHideout_4F_EventScript_OnWarp {
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_FaceUp)
	end
}

script MagmaHideout_4F_EventScript_OnFrameAfterPallet {
    lock
    delay (30)
    msgbox(format("ARIANA: ...\pYears of research and careful planning...\pAll for what?\pTo be ruined by a couple of children...?"))
    waitmessage
    applymovement (SHANE_ID, MagmaHideout_4F_Movement_StepUp)
	waitmovement (0)
    msgbox(format("SHANE: That's right, you old hag!\pJust give it up already!"))
    waitmessage
    closemessage
    delay (30)
    playse (SE_M_EXPLOSION)
    setvar (VAR_0x8004, 1)  // vertical pan
	setvar (VAR_0x8005, 1)  // horizontal pan
	setvar (VAR_0x8006, 12)  // num shakes
	setvar (VAR_0x8007, 5)  // shake delay
	special (ShakeCamera)
	waitstate
    waitse
    playse (SE_PIN)
    applymovement (DAMON_ID, MagmaHideout_4F_Movement_DamonExclamationDown)
    applymovement (SCIENTIST_1_ID, MagmaHideout_4F_Movement_DamonExclamationDown)
    applymovement (SCIENTIST_2_ID, MagmaHideout_4F_Movement_DamonExclamationDown)
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_DamonExclamationDown)
    applymovement (SHANE_ID, MagmaHideout_4F_Movement_DamonExclamationDown)
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_DamonExclamationDown)
	waitmovement (0)
    waitse
    delay (30)
    applymovement (SILVER_ID, MagmaHideout_4F_Movement_PlayerToAriana)
    applymovement (FERALIGATR_ID, MagmaHideout_4F_Movement_PlayerToAriana)
	waitmovement (0)
    msgbox(format("DAD: I rushed over as soon as I got the call, but it looks like the battle is already over.\pSHANE, {PLAYER}, you both did well.\pI'll take it from here."))
    waitmessage
    closemessage
    special (SpawnCameraObject)
    applymovement (SHANE_ID, MagmaHideout_4F_Movement_ShaneOutOfWay)
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_PlayerOutOfWay)
	waitmovement (0)
    special (RemoveCameraObject)
    applymovement (SILVER_ID, MagmaHideout_4F_Movement_SilverToAriana)
    applymovement (FERALIGATR_ID, MagmaHideout_4F_Movement_FeraligatrToAriana)
	waitmovement (0)
    applymovement (SHANE_ID, MagmaHideout_4F_Movement_FaceUp)
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_FaceUp)
	waitmovement (0)
    msgbox(format("DAD: It's been a long time, mother..."))
    waitmessage
    playse (SE_PIN)
    applymovement (SHANE_ID, MagmaHideout_4F_Movement_Question)
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_Question)
	waitmovement (0)
    waitse
    delay (30)
    msgbox(format("ARIANA: So, the Champion himself has decided to grace us with his presence!\pSILVER, dear, you should be on my side...\pI'm only trying to bring your father back to us."))
    waitmessage
    applymovement (SILVER_ID, MagmaHideout_4F_Movement_StepUp)
	waitmovement (0)
    msgbox(format("DAD: When GIOVANNI left, he told me he was going to reform Team Rocket himself, and use the full potential of his subordinates.\pHe would have taken us with him if he thought we had any potential.\pPower is the only thing that man ever wanted, not a family.\pJust accept it, and move on."))
    waitmessage
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_StepDown)
	waitmovement (0)
    fadeoutbgm (1)
    msgbox(format("ARIANA: I refuse...\pI will never give up on GIOVANNI!"))
    waitmessage
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_StepRight)
	waitmovement (0)
    msgbox(format("Start the time machine back up!"))
    waitmessage
    applymovement (SILVER_ID, MagmaHideout_4F_Movement_StepUp)
	waitmovement (0)
    msgbox(format("DAD: Feraligatr!"))
    waitmessage
    closemessage
    applymovement (FERALIGATR_ID, MagmaHideout_4F_Movement_FeraligatrToFront)
	waitmovement (0)
    waitse
	playmoncry (SPECIES_FERALIGATR, CRY_MODE_NORMAL)
    waitmoncry
    applymovement (DAMON_ID, MagmaHideout_4F_Movement_DamonAway)
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_ArianaAway)
	waitmovement (0)
    playse (SE_M_MEGA_KICK)
    applymovement (FERALIGATR_ID, MagmaHideout_4F_Movement_FeraligatrRunForward)
	waitmovement (0)
    waitse
    playse (SE_EFFECTIVE)
    applymovement (FERALIGATR_ID, MagmaHideout_4F_Movement_FeraligatrJumpBack)
	waitmovement (0)
    waitse
    delay (30)
    playse (SE_ICE_CRACK)
    setmetatile (9, 4, 0x258, TRUE)
    setmetatile (9, 3, 0x250, TRUE)
    special (DrawWholeMapView)
    waitse
    delay (30)
    playse (SE_ICE_CRACK)
    setmetatile (8, 5, 0x259, TRUE)
    setmetatile (9, 5, 0x25A, TRUE)
    setmetatile (10, 5, 0x25B, TRUE)
    setmetatile (8, 4, 0x251, TRUE)
    setmetatile (9, 4, 0x252, TRUE)
    setmetatile (10, 4, 0x253, TRUE)
    setmetatile (8, 3, 0x249, TRUE)
    setmetatile (9, 3, 0x24A, TRUE)
    setmetatile (10, 3, 0x24B, TRUE)
    special (DrawWholeMapView)
    waitse
    delay (30)
    addobject (CELEBI_ID)
    playse (SE_ICE_BREAK)
    setmetatile (8, 5, 0x230, TRUE)
    setmetatile (9, 5, 0x231, TRUE)
    setmetatile (10, 5, 0x232, TRUE)
    setmetatile (8, 4, 0x228, TRUE)
    setmetatile (9, 4, 0x229, TRUE)
    setmetatile (10, 4, 0x22A, TRUE)
    setmetatile (8, 3, 0x220, TRUE)
    setmetatile (9, 3, 0x221, TRUE)
    setmetatile (10, 3, 0x222, TRUE)
    special (DrawWholeMapView)
    waitse
    delay (60)
    setflag (FLAG_CELEBI_TANK_BROKEN)
    applymovement (CELEBI_ID, MagmaHideout_4F_Movement_CelebiDownSlow)
	waitmovement (0)
    waitse
    playmoncry (SPECIES_CELEBI, CRY_MODE_NORMAL)
    waitmoncry
    playse (SE_LEDGE)
    applymovement (CELEBI_ID, MagmaHideout_4F_Movement_CelebiJump)
    waitmovement (0)
    playse (SE_LEDGE)
    applymovement (CELEBI_ID, MagmaHideout_4F_Movement_CelebiJump)
    waitmovement (0)
    delay (30)
    applymovement (CELEBI_ID, PetalburgWoods_Movement_CelebiExit)
    waitmovement (0)
    removeobject (CELEBI_ID)
    applymovement (FERALIGATR_ID, MagmaHideout_4F_Movement_FeraligatrBackToSilver)
	waitmovement (0)
    msgbox(format("ARIANA: ..."))
    waitmessage
    closemessage
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_ArianaBackToCenter)
	waitmovement (0)
    msgbox(format("ARIANA: This can't be...\p...\pWhy does fate insist on hindering me at every single step..."))
    waitmessage
    applymovement (SILVER_ID, MagmaHideout_4F_Movement_StepUp)
	waitmovement (0)
    msgbox(format("DAD: It's over, mother."))
    waitmessage
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_StepDown)
	waitmovement (0)
    msgbox(format("DAD: Leave the past where it belongs, and focus on the family standing before you.\pGIOVANNI may not come back, but you still have a chance to connect with your own grandchildren."))
    waitmessage
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_StepDown)
	waitmovement (0)
    msgbox(format("ARIANA: My... grandchildren...\p...\pNo...\pIt's too late for that now!"))
    waitmessage
    playbgm (MUS_RG_ROCKET_HIDEOUT, TRUE)
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_StepRight)
	waitmovement (0)
    msgbox(format("Initiate operation Scorched Earth!"))
    waitmessage
    applymovement (SCIENTIST_2_ID, MagmaHideout_4F_Movement_StepUp)
	waitmovement (0)
    msgbox(format("SCIENTIST: Yes, Ma'am!"))
    waitmessage
    closemessage
    playse (SE_PIN)
    applymovement (SILVER_ID, MagmaHideout_4F_Movement_Exclamation)
	waitmovement (0)
    waitse
    delay (30)
    playse (SE_PC_ON)
    waitse
    delay (30)
    playse (SE_SELECT)
    waitse
    playse (SE_SELECT)
    waitse
    playse (SE_PC_LOGIN)
    waitse
    delay (30)
    setflag (FLAG_HIDE_SILVER_ECRUTEAK)
    warpsilent (MAP_BATTLE_FRONTIER_RANKING_HALL, 8, 13)
    waitstate
    release
    end
}

script MagmaHideout_4F_EventScript_OnFrameAfterEcruteak {
    lock
    delay (30)
    msgbox(format("ARIANA: Hehehe!\pWe rigged the Bell Tower with explosives years ago as a little insurance policy.\pNow, Ho-oh will go on a rampage, incinerating everything in its path!"))
    waitmessage
    applymovement (SILVER_ID, MagmaHideout_4F_Movement_StepUp)
	waitmovement (0)
    msgbox(format("DAD: So, your Celebi plan didn't work out the way you wanted, and now all of Johto must suffer?\pYou disappoint me, mother.\pYou're just like a child throwing a tantrum."))
    waitmessage
    closemessage
    addobject (13)
    addobject (14)
    applymovement (13, MagmaHideout_4F_Movement_PlayerToAriana)
    applymovement (14, MagmaHideout_4F_Movement_PlayerToAriana)
	waitmovement (0)
    applymovement (SILVER_ID, MagmaHideout_4F_Movement_StepDown)
    applymovement (FERALIGATR_ID, MagmaHideout_4F_Movement_StepDown)
    applymovement (SHANE_ID, MagmaHideout_4F_Movement_FaceRight)
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_FaceLeft)
	waitmovement (0)
    msgbox(format("SILVER, sir, thanks for the help.\pWe'll be taking them into custody now.\pDAD: You should be thanking my kids, they did most of the work."))
    waitmessage
    closemessage
    applymovement (FERALIGATR_ID, MagmaHideout_4F_Movement_StepRight)
    applymovement (SILVER_ID, MagmaHideout_4F_Movement_SilverOutOfWay)
    waitmovement (0)
    delay (30)
    applymovement (13, MagmaHideout_4F_Movement_Cop1ToAriana)
    applymovement (14, MagmaHideout_4F_Movement_Cop2ToAriana)
	waitmovement (0)
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_StepDown)
	waitmovement (0)
    msgbox(format("ARIANA: This isn't the last you'll see of Team Phoenix, I guarantee it!"))
    waitmessage
    applymovement (SILVER_ID, MagmaHideout_4F_Movement_StepUp)
	waitmovement (0)
    msgbox(format("DAD: First we'll take care of this Ho-oh mess, and then we'll extinguish any flames that are left of your little gang."))
    waitmessage
    closemessage
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_ArianaExit)
    applymovement (DAMON_ID, MagmaHideout_4F_Movement_DamonExit)
    applymovement (SCIENTIST_1_ID, MagmaHideout_4F_Movement_Scientist1Exit)
    applymovement (SCIENTIST_2_ID, MagmaHideout_4F_Movement_Scientist2Exit)
	waitmovement (0)
    applymovement (13, MagmaHideout_4F_Movement_Cop1Exit)
    applymovement (14, MagmaHideout_4F_Movement_Cop2Exit)
	waitmovement (0)
    removeobject (ARIANA_ID)
    removeobject (DAMON_ID)
    removeobject (SCIENTIST_1_ID)
    removeobject (SCIENTIST_2_ID)
    removeobject (13)
    removeobject (14)
    applymovement (SILVER_ID, MagmaHideout_4F_Movement_SilverBackToCenter)
    waitmovement (0)
    applymovement (FERALIGATR_ID, MagmaHideout_4F_Movement_StepDown)
	applymovement (SHANE_ID, MagmaHideout_4F_Movement_ShaneToCenter)
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_PlayerToCenter)
	waitmovement (0)
    msgbox(format("DAD: Good work handling Team Phoenix, it's good to see you two again.\pUnfortunately, we don't have enough time to catch up.\pWe'll have to take care of this Ho-oh situation as soon as possible."))
    waitmessage
    applymovement (SHANE_ID, MagmaHideout_4F_Movement_ShaneStepAway)
    waitmovement (0)
    delay (20)
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_StepDown)
	waitmovement (0)
    msgbox(format("SHANE: You show up late to the action, and after so many years of being away from home, and then expect us to be glad to see you?\p{PLAYER} can help you with this Ho-oh crap.\pI was only here for one thing, to take down those jerks.\pI'll see you losers later."))
    waitmessage
    applymovement (SILVER_ID, MagmaHideout_4F_Movement_StepDown)
    waitmovement (0)
    msgbox(format("DAD: SHANE!"))
    waitmessage
    closemessage
    applymovement (SHANE_ID, MagmaHideout_4F_Movement_ShaneExit)
	waitmovement (0)
    removeobject (SHANE_ID)
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_StepUp)
	waitmovement (0)
    msgbox(format("DAD: That boy...\pWe'll talk about this later.\p{PLAYER}, I'm going to Ecruteak City to hold off Ho-oh for as long as I can.\pI need to you go to the Whirl Islands.\pThere, you should find the legendary POKéMON, Lugia.\pIf there's anything capable of calming Ho-oh's rampage, perhaps Lugia can...\pI need you to find Lugia in the Whirl Islands and awaken it."))
    waitmessage
    applymovement (SILVER_ID, MagmaHideout_4F_Movement_StepDown)
    waitmovement (0)
    msgbox(format("Oh, and you'll need this!"))
    waitmessage
    giveitem (ITEM_HM08_DIVE)
    msgbox(format("DAD: That HM contains the move Dive.\pThe whirlpools around the Whirl Islands are impassible, but that HM will allow you to dive underneath them.\pGood luck, {PLAYER}, I know you can do it."))
    waitmessage
    closemessage
    applymovement (FERALIGATR_ID, MagmaHideout_4F_Movement_FeraligatrExit)
    applymovement (SILVER_ID, MagmaHideout_4F_Movement_SilverExit)
    waitmovement (0)
    removeobject (SILVER_ID)
    removeobject (FERALIGATR_ID)
    setflag (FLAG_HIDE_SILVER_RAGE_HIDEOUT)
    setflag (FLAG_HIDE_MAGMA_HIDEOUT_GRUNTS)
    clearflag (FLAG_HIDE_SILVER_ECRUTEAK)
    addvar (VAR_ECRUTEAK_STATE, 1)
    release
    end
}

movement MagmaHideout_4F_Movement_FeraligatrExit {
    walk_right * 2
    walk_down * 3
    walk_left
    walk_down * 5
}

movement MagmaHideout_4F_Movement_SilverExit {
    walk_right
    walk_down * 3
    walk_left
    walk_down * 5
}

movement MagmaHideout_4F_Movement_ShaneExit {
    walk_down
    walk_right
    walk_down * 5
}

movement MagmaHideout_4F_Movement_ShaneStepAway {
    face_up
    lock_facing_direction
    walk_down
    unlock_facing_direction
}

movement MagmaHideout_4F_Movement_ArianaExit {
    walk_down * 9
}

movement MagmaHideout_4F_Movement_ShaneToCenter {
    walk_right
    walk_in_place_up
}

movement MagmaHideout_4F_Movement_PlayerToCenter {
    walk_left
    walk_in_place_up
}

movement MagmaHideout_4F_Movement_DamonExit {
    walk_right * 2
    walk_down * 9
}

movement MagmaHideout_4F_Movement_Scientist1Exit {
    walk_right * 3
    walk_down * 9
}

movement MagmaHideout_4F_Movement_Scientist2Exit {
    walk_left * 2
    delay_16
    walk_left
    walk_down * 9
}

movement MagmaHideout_4F_Movement_Cop1Exit {
    walk_right * 2
    walk_down * 9
}

movement MagmaHideout_4F_Movement_Cop2Exit {
    walk_left
    walk_down * 9
}

movement MagmaHideout_4F_Movement_FaceRight {
    face_right
}

movement MagmaHideout_4F_Movement_SilverOutOfWay {
    walk_right
    walk_in_place_left
}

movement MagmaHideout_4F_Movement_Cop1ToAriana {
    walk_up * 2
    walk_left * 2
    walk_in_place_right
}

movement MagmaHideout_4F_Movement_Cop2ToAriana {
    walk_up * 3
    walk_right
    walk_in_place_left
}

movement MagmaHideout_4F_Movement_SilverBackToCenter {
    walk_left
    walk_in_place_down
}

movement MagmaHideout_4F_Movement_ShaneOutOfWay {
    walk_left
    walk_down
    face_right
}

movement MagmaHideout_4F_Movement_PlayerOutOfWay {
    walk_right
    walk_down
    face_left
}

movement MagmaHideout_4F_Movement_CameraToPlayer {
    walk_faster_right
    walk_faster_down
}

movement MagmaHideout_4F_Movement_SilverToAriana {
    walk_up
}

movement MagmaHideout_4F_Movement_FeraligatrToAriana {
    walk_up
    walk_left
    walk_up
}

movement MagmaHideout_4F_Movement_Question {
    emote_question_mark
}

movement MagmaHideout_4F_Movement_FeraligatrToFront {
    walk_fast_up
    walk_fast_right
    walk_in_place_up
}

movement MagmaHideout_4F_Movement_DamonAway {
    walk_fast_left
    walk_in_place_right
}

movement MagmaHideout_4F_Movement_ArianaAway {
    walk_fast_right * 2
    walk_in_place_left
}

movement MagmaHideout_4F_Movement_FeraligatrRunForward {
    walk_in_place_fast_up * 8
    walk_fast_up * 2
}

movement MagmaHideout_4F_Movement_FeraligatrJumpBack {
    lock_facing_direction
    jump_2_down
    unlock_facing_direction
    face_up
}

movement MagmaHideout_4F_Movement_CelebiDownSlow {
    walk_slow_down
}

movement MagmaHideout_4F_Movement_CelebiJump {
    jump_in_place_down
}

movement PetalburgWoods_Movement_CelebiExit {
    lock_facing_direction
    walk_up
    walk_fast_up * 5
    unlock_facing_direction
}

movement MagmaHideout_4F_Movement_FeraligatrBackToSilver {
    walk_left
    walk_down
    walk_in_place_up
}

movement MagmaHideout_4F_Movement_ArianaBackToCenter {
    walk_left * 2
    walk_in_place_up
}

script MagmaHideout_4F_EventScript_MainTrigger {
    lock
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_PlayerToAriana)
	waitmovement (0)
    playse (SE_PIN)
    applymovement (DAMON_ID, MagmaHideout_4F_Movement_DamonExclamationDown)
    applymovement (SCIENTIST_1_ID, MagmaHideout_4F_Movement_DamonExclamationDown)
    applymovement (SCIENTIST_2_ID, MagmaHideout_4F_Movement_DamonExclamationDown)
	waitmovement (0)
    waitse
    delay (30)
    applymovement (DAMON_ID, MagmaHideout_4F_Movement_StepRight)
	waitmovement (0)
    msgbox(format("DAMON: Boss...\p...\p???: Your persistence certainly lives up to the rumors."))
    waitmessage
    applymovement (DAMON_ID, MagmaHideout_4F_Movement_FaceDown)
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_FaceDown)
	waitmovement (0)
    msgbox(format("Fortunately, we are always one step ahead of you...\pSHANE: Hey assholes!"))
    waitmessage
    closemessage
    playse (SE_PIN)
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_DamonExclamationDown)
	waitmovement (0)
    waitse
    delay (30)
    addobject (SHANE_ID)
    applymovement (SHANE_ID, MagmaHideout_4F_Movement_ShaneEnter)
	waitmovement (0)
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_FaceLeft)
	waitmovement (0)
    msgbox(format("SHANE: I don't know how you managed to get here before me, {PLAYER}.\pIt's going to be really tough to make good on my bet with you..."))
    waitmessage
    closemessage
    applymovement (SHANE_ID, MagmaHideout_4F_Movement_FaceUp)
    applymovement (OBJ_EVENT_ID_PLAYER, MagmaHideout_4F_Movement_FaceUp)
	waitmovement (0)
    delay (30)
    msgbox(format("???: So, you're both here.\pHow wonderful...\pI suppose I should introduce myself.\pMy name is ARIANA.\pI formed Team Phoenix after the fall of Team Rocket, nearly twenty years ago."))
    waitmessage
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_StepRight)
	waitmovement (0)
    msgbox(format("Team Rocket was an unstoppable force back then.\pOur power and influence felt unlimited."))
    waitmessage
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_StepDown)
	waitmovement (0)
    msgbox(format("It was all thanks to our leader, the great GIOVANNI!\pHe had an absolute mastery over POKéMON, the likes of which the world had never seen.\pI knew everything would be perfect as long as I kept by his side...\p...\pBut then, that brat from Pallet Town appeared..."))
    waitmessage
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_StepDown)
	waitmovement (0)
    msgbox(format("That brat ruined everything!\pHe took down Team Rocket overnight, we were absolutely humiliated!\p...\pThen... our GIOVANNI left us...\pConsumed by a profound sense of uncertainty and doubt, GIOVANNI disbanded Team Rocket and vanished from the world, and my life...\pAt the time, I tried patching the gang back together, but that too, ended in failure...\pWithout GIOVANNI'S guidance, Team Rocket was just an empty husk of what it once was..."))
    waitmessage
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_StepUp)
	waitmovement (0)
    msgbox(format("That's where Celebi comes in...\pI'm going to use Celebi's time traveling abilities to ensure that RED brat never gets the chance!"))
    waitmessage
    applymovement (SHANE_ID, MagmaHideout_4F_Movement_StepUp)
	waitmovement (0)
    msgbox(format("SHANE: Blah, blah, blah!"))
    waitmessage
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_StepDown)
	waitmovement (0)
    msgbox(format("Don't you ever shut up, you old hag?"))
    waitmessage
    playse (SE_PIN)
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_Exclamation)
	waitmovement (0)
    waitse
    delay (30)
    msgbox(format("SHANE: Nobody cares about your sob story.\pIt doesn't matter, because we're going to take you down right now!"))
    waitmessage
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_StepDown)
	waitmovement (0)
    msgbox(format("ARIANA: Hehe! You sure are lively!\pAs much as I would love to teach you some manners, you arrived a little too late.\pOur time machine is now fully operational!"))
    waitmessage
    applymovement (ARIANA_ID, MagmaHideout_4F_Movement_StepRight)
	waitmovement (0)
    msgbox(format("Now, it is time!\pActivate the machine!"))
    waitmessage
    applymovement (SCIENTIST_2_ID, MagmaHideout_4F_Movement_StepUp)
	waitmovement (0)
    msgbox(format("SCIENTIST: Yes, Ma'am!"))
    waitmessage
    closemessage
    fadeoutbgm (1)
    playse (SE_PC_ON)
    waitse
    delay (30)
    playse (SE_SELECT)
    waitse
    playse (SE_SELECT)
    waitse
    playse (SE_PC_LOGIN)
    waitse
    delay (30)
    fadescreenspeed (FADE_TO_WHITE, 8)
    playbgm (MUS_RAYQUAZA_APPEARS, FALSE)
    checkplayergender
    if (var(VAR_RESULT) == MALE) {
        setvar (VAR_0x8004, MALE)
    }
    elif (var(VAR_RESULT) == FEMALE) {
        setvar (VAR_0x8004, FEMALE)
    }
    setwarp (MAP_AQUA_HIDEOUT_UNUSED_RUBY_MAP1, 5, 8)
    special (TimeTravelBackClock)
	waitstate
    fadedefaultbgm
    release
    end
}

movement MagmaHideout_4F_Movement_PlayerToAriana {
    walk_up * 7
}

movement MagmaHideout_4F_Movement_DamonExclamationDown {
    emote_exclamation_mark
    walk_in_place_down
}

movement MagmaHideout_4F_Movement_Exclamation {
    emote_exclamation_mark
}

movement MagmaHideout_4F_Movement_StepRight {
    walk_in_place_right
}

movement MagmaHideout_4F_Movement_FaceDown {
    face_down
}

movement MagmaHideout_4F_Movement_ShaneEnter {
    walk_up * 4
    walk_left
    walk_up * 2
    face_right
}

movement MagmaHideout_4F_Movement_FaceLeft {
    face_left
}

movement MagmaHideout_4F_Movement_FaceUp {
    face_up
}

movement MagmaHideout_4F_Movement_StepDown {
    walk_in_place_down
}

movement MagmaHideout_4F_Movement_StepUp {
    walk_in_place_up
}

raw`
MagmaHideout_4F_EventScript_Grunt11::
	trainerbattle_single TRAINER_GRUNT_MAGMA_HIDEOUT_11, MagmaHideout_4F_Text_Grunt11Intro, MagmaHideout_4F_Text_Grunt11Defeat
	msgbox MagmaHideout_4F_Text_Grunt11PostBattle, MSGBOX_AUTOCLOSE
	end

MagmaHideout_4F_EventScript_Grunt12::
	trainerbattle_single TRAINER_GRUNT_MAGMA_HIDEOUT_12, MagmaHideout_4F_Text_Grunt12Intro, MagmaHideout_4F_Text_Grunt12Defeat
	msgbox MagmaHideout_4F_Text_Grunt12PostBattle, MSGBOX_AUTOCLOSE
	end

MagmaHideout_4F_EventScript_Grunt13::
	trainerbattle_single TRAINER_GRUNT_MAGMA_HIDEOUT_13, MagmaHideout_4F_Text_Grunt13Intro, MagmaHideout_4F_Text_Grunt13Defeat
	msgbox MagmaHideout_4F_Text_Grunt13PostBattle, MSGBOX_AUTOCLOSE
	end

MagmaHideout_4F_EventScript_Tabitha::
	trainerbattle_single TRAINER_TABITHA_MAGMA_HIDEOUT, MagmaHideout_4F_Text_TabithaIntro, MagmaHideout_4F_Text_TabithaDefeat
	msgbox MagmaHideout_4F_Text_TabithaPostBattle, MSGBOX_AUTOCLOSE
	end

MagmaHideout_4F_Text_Grunt11Intro:
	.string "Just turn back now kid, none of this\n"
	.string "concerns you!$"

MagmaHideout_4F_Text_Grunt11Defeat:
	.string "You were just too much for me to handle.$"

MagmaHideout_4F_Text_Grunt11PostBattle:
	.string "What is so wrong about wanting\n"
	.string "unlimited power and money?$"

MagmaHideout_4F_Text_Grunt12Intro:
	.string "Muahaha, very soon Team Phoenix will\n"
	.string "become the most powerful organization\l"
	.string "in the world!$"

MagmaHideout_4F_Text_Grunt12Defeat:
	.string "Stop trying to ruin our plans!$"

MagmaHideout_4F_Text_Grunt12PostBattle:
	.string "I'm sorry boss, I couldn't hold off the\n"
	.string "intruder very long.$"

MagmaHideout_4F_Text_Grunt13Intro:
	.string "I will stop you no matter what!\p"
	.string "For Team Phoenix!$"

MagmaHideout_4F_Text_Grunt13Defeat:
	.string "I have failed...$"

MagmaHideout_4F_Text_Grunt13PostBattle:
	.string "You're dead wrong if you think this is\n"
	.string "the end of Team Phoenix!$"

MagmaHideout_4F_Text_TabithaIntro:
	.string "Hehehe!\n"
	.string "You made it this far, so I'll tell you!\p"
	.string "That's right!\n"
	.string "Up ahead, GROUDON is sleeping!\p"
	.string "MAXIE went to GROUDON just seconds\n"
	.string "ago!\p"
	.string "It's going to awaken real soon!\n"
	.string "Hehe! Hehehe!$"

MagmaHideout_4F_Text_TabithaDefeat:
	.string "Taken down again…\n"
	.string "Hehe…$"

MagmaHideout_4F_Text_TabithaPostBattle:
	.string "…And while you wasted time with me,\n"
	.string "MAXIE should have awakened GROUDON…$"
`