const SHANE_ID = 1
const THORNE_ID = 2

mapscripts Route110_TrickHouseCorridor_MapScripts {
    MAP_SCRIPT_ON_LOAD {
        call_if_set (FLAG_PHQ_CELLDOOR_1_OPEN, Route110_TrickHouseCorridor_Tiles_Door_1)
        call_if_set (FLAG_PHQ_CELLDOOR_3_OPEN, Route110_TrickHouseCorridor_Tiles_Door_3)
        end
    }
}

script Route110_TrickHouseCorridor_Tiles_Door_1 {
    setmetatile (7, 7, 0x341, FALSE)
    setmetatile (7, 6, 0x357, FALSE)
    setmetatile (7, 5, 0x34F, FALSE)
    setmetatile (6, 6, 0x356, TRUE)
    setmetatile (6, 5, 0x34E, FALSE)
    return
}

script Route110_TrickHouseCorridor_Tiles_Door_3 {
    setmetatile (15, 7, 0x341, FALSE)
    setmetatile (15, 6, 0x357, FALSE)
    setmetatile (15, 5, 0x34F, FALSE)
    setmetatile (14, 6, 0x356, TRUE)
    setmetatile (14, 5, 0x34E, FALSE)
    return
}

script Route110_TrickHouseCorridor_EventScript_Trigger1 {
    lock
    applymovement (OBJ_EVENT_ID_PLAYER, Route110_TrickHouseCorridor_Movement_PtoThorneTop)
    waitmovement (0)
    goto (Route110_TrickHouseCorridor_EventScript_Main)
    end
}

script Route110_TrickHouseCorridor_EventScript_Trigger2 {
    lock
    applymovement (OBJ_EVENT_ID_PLAYER, Route110_TrickHouseCorridor_Movement_PtoThorneMiddle)
    waitmovement (0)
    goto (Route110_TrickHouseCorridor_EventScript_Main)
    end
}

script Route110_TrickHouseCorridor_EventScript_Trigger3 {
    lock
    applymovement (OBJ_EVENT_ID_PLAYER, Route110_TrickHouseCorridor_Movement_PtoThorneBottom)
    waitmovement (0)
    goto (Route110_TrickHouseCorridor_EventScript_Main)
    end
}

movement Route110_TrickHouseCorridor_Movement_PtoThorneTop {
    walk_down
    walk_right
}

movement Route110_TrickHouseCorridor_Movement_PtoThorneMiddle {
    walk_right
}

movement Route110_TrickHouseCorridor_Movement_PtoThorneBottom {
    walk_up
    walk_right
}

script Route110_TrickHouseCorridor_EventScript_Main {
    waitse
    playse (SE_PIN)
    applymovement (THORNE_ID, Route110_TrickHouseCorridor_Movement_QuestionLeft)
    waitmovement (0)
    waitse
    delay (60)
    msgbox(format("???: Oh? And who might you be?"))
    waitmessage
    closemessage
    applymovement (THORNE_ID, Route110_TrickHouseCorridor_Movement_ThorneDownLeft)
    waitmovement (0)
    msgbox(format("???: You must be the other child I was told about, but you're supposed to be locked in a cell at our Goldenrod base.\p...\pI see, I'm impressed you found this place.\pMy name is THORNE, I'm one of the TEAM PHOENIX ADMINS. I believe you've already met DAMON.\p...\pI won't pretend we are a righteous group."))
    waitmessage
    applymovement (THORNE_ID, Route110_TrickHouseCorridor_Movement_StepInPlaceLeft)
    waitmovement (0)
    msgbox(format("Far from it in fact.\pAs you may have guessed, we exploit POKÃ©MON for our own personal gain.\pYou see, TEAM PHOENIX was actually born from the ashes of TEAM ROCKET, have you heard of them?\pNo, I suppose you wouldn't have, they were disbanded many years ago.\pAt the time, I was one of TEAM ROCKETS top scientists, and I am still very loyal to thier organization.\pNow, to complete our ultimate goal, you need to come with me!"))
    applymovement (THORNE_ID, Route110_TrickHouseCorridor_Movement_ThorneStepLeft)
    waitmovement (0)
    trainerbattle_no_intro (TRAINER_BRENDAN_ROUTE_119_MUDKIP, Route110_TrickHouseCorridor_Text_ThorneDefeat)
    msgbox(format("You may have won this round, but TEAM PHOENIX is far from done.\pOur boss will never relent, and TEAM PHOENIX will surely reign supreme one day!"))
    waitmessage
    closemessage
    applymovement (THORNE_ID, Route110_TrickHouseCorridor_Movement_ThorneExit)
    waitmovement (0)
    removeobject (THORNE_ID)
    applymovement (OBJ_EVENT_ID_PLAYER, Route110_TrickHouseCorridor_Movement_PtoCell)
    waitmovement (0)
    msgbox(format("{PLAYER} inserted the Cell Key into the door."))
    waitmessage
    closemessage
    delay (30)
    playse (SE_DOOR)
    setmetatile (7, 7, 0x341, FALSE)
    setmetatile (7, 6, 0x357, FALSE)
    setmetatile (7, 5, 0x34F, FALSE)
    setmetatile (6, 6, 0x356, TRUE)
    setmetatile (6, 5, 0x34E, FALSE)
    special (DrawWholeMapView)
    setflag(FLAG_PHQ_CELLDOOR_1_OPEN)
    delay (30)
    applymovement (SHANE_ID, Route110_TrickHouseCorridor_Movement_ShaneWalkDown)
    waitmovement (0)
    applymovement (SHANE_ID, Route110_TrickHouseCorridor_Movement_ShaneWalkDownOne)
    applymovement (OBJ_EVENT_ID_PLAYER, Route110_TrickHouseCorridor_Movement_PlayerBackUp)
    waitmovement (0)
    msgbox(format("SHANE: Lets get this straight, I don't need help from the likes of you!\pI was about to bust myself out right before you showed up...\pHow did you get here anyway...?\pWell, it doesn't matter!"))
    waitmessage
    closemessage
    applymovement (SHANE_ID, Route110_TrickHouseCorridor_Movement_ShaneWalkLeft)
    waitmovement (0)
    applymovement (OBJ_EVENT_ID_PLAYER, Route110_TrickHouseCorridor_Movement_StepInPlaceLeft)
    waitmovement (0)
    msgbox(format("SHANE: I don't know why I covered for you back in Goldenrod...\pJust shut up about it, alright!"))
    waitmessage
    closemessage
    applymovement (SHANE_ID, Route110_TrickHouseCorridor_Movement_ShaneExit)
    waitmovement (0)
    removeobject (SHANE_ID)
    setflag (FLAG_HIDE_ECRUTEAK_PHOENIX)
    clearflag (FLAG_HIDE_STRENGTH_KIMONO)
    addvar (VAR_ECRUTEAK_STATE, 1)
    release
    end
}

movement Route110_TrickHouseCorridor_Movement_QuestionLeft {
    emote_question_mark
    walk_in_place_left
}

movement Route110_TrickHouseCorridor_Movement_ThorneDownLeft {
    walk_down
    walk_left
}

movement Route110_TrickHouseCorridor_Movement_StepInPlaceLeft {
    walk_in_place_left
}

movement Route110_TrickHouseCorridor_Movement_ThorneStepLeft {
    walk_left
}

movement Route110_TrickHouseCorridor_Movement_ThorneExit {
    walk_down
    walk_left * 3
    walk_up * 7
}

movement Route110_TrickHouseCorridor_Movement_PtoCell {
    walk_right * 3
    walk_up
}

movement Route110_TrickHouseCorridor_Movement_ShaneWalkDown {
    walk_down * 2
}

movement Route110_TrickHouseCorridor_Movement_ShaneWalkDownOne {
    walk_down
}

movement Route110_TrickHouseCorridor_Movement_PlayerBackUp {
    lock_facing_direction
    walk_down
    unlock_facing_direction
    face_up
}

movement Route110_TrickHouseCorridor_Movement_ShaneWalkLeft {
    walk_left * 3
    delay_16
    walk_in_place_right
}

movement Route110_TrickHouseCorridor_Movement_ShaneExit {
    walk_left * 2
    walk_up * 6
}

text Route110_TrickHouseCorridor_Text_ThorneDefeat {
    format("A minor setback...")
}

script Route110_TrickHouseCorridor_EventScript_Door3 {
    lock
    if (flag(FLAG_PHQ_CELLDOOR_3_OPEN)) {
        msgbox(format("The cell door is open."))
        release
        end
    }
    else {
        checkitem (ITEM_BASEMENT_KEY)
        if (var(VAR_RESULT) == TRUE) {
            msgbox(format("{PLAYER} inserted the Cell Key into the door."))
            waitmessage
            closemessage
            delay (30)
            playse (SE_DOOR)
            setmetatile (15, 7, 0x341, FALSE)
            setmetatile (15, 6, 0x357, FALSE)
            setmetatile (15, 5, 0x34F, FALSE)
            setmetatile (14, 6, 0x356, TRUE)
            setmetatile (14, 5, 0x34E, FALSE)
            special (DrawWholeMapView)
            setflag(FLAG_PHQ_CELLDOOR_3_OPEN)
            release
            end
        }
        else {
            msgbox(format("The cell door is locked."))
            release
            end
        }
    }
}