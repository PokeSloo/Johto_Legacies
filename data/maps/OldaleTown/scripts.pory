const SHANE_ID = 4
const BATTLE_SHANE_ID = 5

mapscripts OldaleTown_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setflag (FLAG_VISITED_OLDALE_TOWN)
        end
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_CHERRYGROVE_STATE, 1: OldaleTown_OnFrame
    ]
}

script OldaleTown_OnFrame {
    lock
    playse (SE_PIN)
    applymovement (SHANE_ID, OldaleTown_ShaneApostraphe)
    waitmovement (0)
    delay (10)
    msgbox (format("SHANE: What took you so long?"))
    waitmessage
    closemessage
    applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_PlayerStepToShane)
    waitmovement (0)
    msgbox (format("Any longer, and I was going to leave you behind.\pCome on, let's get this over with."))
    waitmessage
    closemessage
	applymovement (SHANE_ID, OldaleTown_ShaneWalktoNerd)
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_PlayerWalktoNerd)
    waitmovement (0)
	msgbox (format("Pfft, get a load of this nerd. Not a single girl would go for a guy like that.\pTrust me."))
    waitmessage
    closemessage
	applymovement (SHANE_ID, OldaleTown_ShaneWalktoGrass)
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_PlayerWalktoGrass)
    waitmovement (0)
    msgbox (format("This is a patch of tall grass. A scrawny kid like you would get ripped to shreds if you stepped one foot in there without a POKéMON of your own."))
    waitmessage
    closemessage
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_PlayerWalkDownOne)
    waitmovement (0)
	applymovement (SHANE_ID, OldaleTown_ShaneWalktoLedge, MAP_OLDALE_TOWN)
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_PlayerWalktoLedge)
    waitmovement (0)
	delay (20)
	msgbox (format("I jump down from small ledges like this all the time."))
    waitmessage
	applymovement (SHANE_ID, OldaleTown_ShaneFacePlayer, MAP_OLDALE_TOWN)
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_PlayerFaceShane)
    waitmovement (0)
	msgbox (format("I bet even a wimp like you could jump it."))
    waitmessage
    closemessage
	applymovement (SHANE_ID, OldaleTown_ShaneFaceDown, MAP_OLDALE_TOWN)
    waitmovement (0)
	delay (30)
	applymovement (SHANE_ID, OldaleTown_ShaneJumpLedge, MAP_OLDALE_TOWN)
	playse (SE_LEDGE)
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_ShaneFaceDown)
    waitmovement (0)
	delay (30)
	msgbox (format("You'd better hurry up, or I'll leave you behind."))
    waitmessage
    closemessage
	applymovement (SHANE_ID, OldaleTown_ShaneWalkOffscreen, MAP_OLDALE_TOWN)
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_PlayerPrepareJumpLedge)
	waitmovement (0)
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_PlayerJumpLedge)
	playse (SE_LEDGE)
    waitmovement (0)
	msgbox (format("Didn't break your ankles, did you? Ha!"))
    waitmessage
    closemessage
	applymovement (SHANE_ID, OldaleTown_ShaneToTree, MAP_OLDALE_TOWN)
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_PlayerToTree)
    waitmovement (0)
	msgbox (format("These small trees are super annoying. If you have HM01 and the Violet City gym badge, you can cut them down."))
    waitmessage
    closemessage
	applymovement (SHANE_ID, OldaleTown_ShaneWalkAroundTrees, MAP_OLDALE_TOWN)
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_PlayerWalkAroundTrees)
    waitmovement (0)
	msgbox (format("This part is a little tricky, just try and keep up."))
    waitmessage
    closemessage
	applymovement (SHANE_ID, OldaleTown_ShaneWalkToSign, MAP_OLDALE_TOWN)
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_PlayerWalkToSign)
    waitmovement (0)
	applymovement (SHANE_ID, OldaleTown_ShaneJumpRight, MAP_OLDALE_TOWN)
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_PlayerJumpRight)
	playse (SE_LEDGE)
	playse (SE_LEDGE)
    waitmovement (0)
	msgbox (format("Wow, I'm surprised you didn't get left in my dust, haha!"))
    waitmessage
    closemessage
	applymovement (SHANE_ID, OldaleTown_ShaneWalkToLab, MAP_OLDALE_TOWN)
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_PlayerWalkToLab)
    waitmovement (0)
	delay (20)
	applymovement (SHANE_ID, OldaleTown_ShaneFacePlayer, MAP_OLDALE_TOWN)
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_PlayerFaceShane)
    waitmovement (0)
	msgbox (format("This is PROF. ELM's lab. He's also a huge nerd, so you two should get along.\pWelp, Mom just said to bring you here, she didn't say anything about bringing you back.\pYou're on your own now, later loser."))
    waitmessage
    closemessage
	applymovement (SHANE_ID, OldaleTown_ShaneLeaves, MAP_OLDALE_TOWN)
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_PlayerWatchesShane)
    waitmovement (0)
	removeobject (SHANE_ID, MAP_OLDALE_TOWN)
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_BothFaceUp)
    waitmovement (0)
    setflag (FLAG_HIDE_SHANE_CHERRYGROVE_ESCORT)
    addvar (VAR_CHERRYGROVE_STATE, 1)
    release
    end
}

movement OldaleTown_ShaneApostraphe {
    face_left
    emote_exclamation_mark
}

movement OldaleTown_PlayerStepToShane {
    walk_right * 3
}

movement OldaleTown_ShaneWalktoNerd {
	walk_right * 9
	face_down
}

movement OldaleTown_PlayerWalktoNerd {
	walk_right * 9
	delay_16
	face_down
}

movement OldaleTown_ShaneWalktoGrass {
	walk_right * 6
	walk_up
	walk_right * 18
	walk_down * 2
	walk_right
	walk_down
}

movement OldaleTown_PlayerWalktoGrass {
	walk_right * 7
	walk_up
	walk_right * 18
	walk_down * 2
	walk_right
	face_down
	delay_16
	walk_left
	face_down
}

movement OldaleTown_ShaneWalktoLedge {
	walk_right * 23
	walk_down * 3
	walk_right
	face_down
}

movement OldaleTown_PlayerWalkDownOne {
	walk_down
}

movement OldaleTown_PlayerWalktoLedge {
	walk_right * 24
	walk_down * 3
}

movement OldaleTown_ShaneFacePlayer {
	face_left
}

movement OldaleTown_PlayerFaceShane {
	face_right
}

movement OldaleTown_ShaneFaceDown {
	face_down
}

movement OldaleTown_ShaneJumpLedge {
	jump_2_down
	delay_16
	walk_in_place_up
}

movement OldaleTown_ShaneWalkOffscreen {
	walk_right * 6
	face_left
}

movement OldaleTown_PlayerPrepareJumpLedge {
	delay_16
	delay_16
	walk_in_place_fast_right
	delay_16
	walk_in_place_fast_down
	delay_16
	walk_in_place_fast_right
	delay_8
	walk_in_place_fast_down
	delay_8
	walk_in_place_fast_down
	walk_in_place_fast_down
	walk_in_place_fast_down
}

movement OldaleTown_PlayerJumpLedge {
	jump_2_down
	player_run_right * 4
	walk_right * 2
}

movement OldaleTown_ShaneToTree {
	walk_right * 5
	face_up
}

movement OldaleTown_PlayerToTree {
	walk_right * 5
	delay_16
	face_up
}

movement OldaleTown_BothFaceUp {
	face_up
}

movement OldaleTown_ShaneWalkAroundTrees {
	walk_right * 12
	walk_up * 2
	walk_right * 4
	walk_up * 4
	walk_right * 6
	delay_16
	face_left
}

movement OldaleTown_PlayerWalkAroundTrees {
	walk_right * 13
	walk_up * 2
	walk_right * 4
	walk_up * 4
	walk_right * 5
}

movement OldaleTown_ShaneWalkToSign {
	walk_fast_down * 4
	walk_fast_right * 7
	walk_fast_up * 5
	walk_fast_right
}

movement OldaleTown_ShaneJumpRight {
	jump_2_right
	walk_fast_right * 10
	walk_right * 2
	walk_up * 2
	face_down
}

movement OldaleTown_PlayerWalkToSign {
	walk_fast_right
	walk_fast_down * 4
	walk_fast_right * 7
	walk_fast_up * 5
	face_right
}

movement OldaleTown_PlayerJumpRight {
	delay_8
	walk_fast_right
	jump_2_right
	walk_fast_right * 10
	walk_right * 2
	walk_up
}

movement OldaleTown_ShaneWalkToLab {
	walk_right * 16
	walk_up
	walk_right * 15
	walk_up
}

movement OldaleTown_PlayerWalkToLab {
	walk_up
	walk_right * 16
	walk_up
	walk_right * 14
	walk_up
}

movement OldaleTown_ShaneLeaves {
	walk_down
	walk_left * 10
}

movement OldaleTown_PlayerWatchesShane {
	delay_16
	face_down
	delay_16 * 3
	face_left
	delay_16 * 9
}

script OldaleTown_EventScript_Girl {
	msgbox (format("If you have a POKéMON that knows the move Surf, and the Olivine City gym badge, then it can ferry you across the water.\pIt's amazing how much more that opens up the world."), MSGBOX_NPC)
	end
}

script OldaleTown_EventScript_Mailbox {
	msgbox ("{PLAYER}'S HOUSE", MSGBOX_SIGN)
	end
}

script OldaleTown_EventScript_Trigger1 {
	lock
	playse (SE_PIN)
    applymovement (BATTLE_SHANE_ID, OldaleTown_ShaneApostraphePlain)
    waitmovement (0)
    delay (30)
	playbgm (MUS_HG_ENCOUNTER_RIVAL, FALSE)
	applymovement (BATTLE_SHANE_ID, OldaleTown_ShaneWalktoPlayer1)
    waitmovement (0)
	goto (OldaleTown_EventScript_ShaneBattle)
}

script OldaleTown_EventScript_Trigger2 {
	lock
	playse (SE_PIN)
    applymovement (BATTLE_SHANE_ID, OldaleTown_ShaneApostraphePlain)
    waitmovement (0)
    delay (30)
	playbgm (MUS_HG_ENCOUNTER_RIVAL, FALSE)
	applymovement (BATTLE_SHANE_ID, OldaleTown_ShaneWalktoPlayer2)
    waitmovement (0)
	goto (OldaleTown_EventScript_ShaneBattle)
}

script OldaleTown_EventScript_Trigger3 {
	lock
	playse (SE_PIN)
    applymovement (BATTLE_SHANE_ID, OldaleTown_ShaneApostraphePlain)
    waitmovement (0)
    delay (30)
	playbgm (MUS_HG_ENCOUNTER_RIVAL, FALSE)
	applymovement (BATTLE_SHANE_ID, OldaleTown_ShaneWalktoPlayer3)
    waitmovement (0)
	goto (OldaleTown_EventScript_ShaneBattle)
}

script OldaleTown_EventScript_Trigger4 {
	lock
	playse (SE_PIN)
    applymovement (BATTLE_SHANE_ID, OldaleTown_ShaneApostraphePlain)
    waitmovement (0)
    delay (30)
	playbgm (MUS_HG_ENCOUNTER_RIVAL, FALSE)
	applymovement (BATTLE_SHANE_ID, OldaleTown_ShaneWalktoPlayer4)
    waitmovement (0)
	goto (OldaleTown_EventScript_ShaneBattle)
}

script OldaleTown_EventScript_ShaneBattle{
	setrespawn (HEAL_LOCATION_LITTLEROOT_TOWN_BRENDANS_HOUSE_2F)
	addvar (VAR_CHERRYGROVE_STATE, 1)
	msgbox (format("SHANE: Wow! You actually made it back on your own.\pI see you got a POKéMON from PROF. ELM. You know, I got one from him too, but it was too weak. I got rid of that thing.\pHey, let's test our your new POKéMON! I'll see if it's a weakling!"))
	trainerbattle_no_intro (TRAINER_BRENDAN_ROUTE_103_TREECKO, OldaleTown_Text_ShaneDefeat)
	msgbox (format ("You'll pay for cheating!"))
	waitmessage
	closemessage
	applymovement (BATTLE_SHANE_ID, OldaleTown_ShaneHitPlayer)
    waitmovement (0)
	playse (SE_WALL_HIT)
	applymovement (OBJ_EVENT_ID_PLAYER, OldaleTown_PlayerKnockBack)
    waitmovement (0)
	special (HealPlayerParty)
	fadescreenspeed (FADE_TO_BLACK, 4)
	delay (30)
	warpsilent (MAP_LITTLEROOT_TOWN_BRENDANS_HOUSE_2F, 1, 4)
	waitstate
	release
	end
}

text OldaleTown_Text_ShaneDefeat {
	"You cheated!"
}

movement OldaleTown_ShaneApostraphePlain {
	emote_exclamation_mark
}

movement OldaleTown_ShaneWalktoPlayer1 {
	walk_right * 6
}

movement OldaleTown_ShaneWalktoPlayer2 {
	walk_down
	walk_right * 6
}

movement OldaleTown_ShaneWalktoPlayer3 {
	walk_down * 2
	walk_right * 6
}

movement OldaleTown_ShaneWalktoPlayer4 {
	walk_down * 3
	walk_right * 6
}

movement OldaleTown_ShaneHitPlayer {
	lock_facing_direction
	walk_slow_left
	delay_16
	walk_faster_right
}

movement OldaleTown_PlayerKnockBack {
	lock_facing_direction
	jump_2_right
}

script OldaleTown_EventScript_ItemTM27 {
	finditem (ITEM_TM27)
	end
}

script OldaleTown_EventScript_ItemMysticWater {
	finditem (ITEM_MYSTIC_WATER)
	end
}

script OldaleTown_EventScript_ItemPrismScale {
	finditem (ITEM_PRISM_SCALE)
	end
}

script OldaleTown_EventScript_FootprintsMan {
	lock
	faceplayer
	if (flag(FLAG_RECEIVED_STEELIXITE)) {
		msgbox(format("Man, it gets exhausting having so many girls chasing after me."))
		release
		end
	}
	elif (flag(FLAG_FIND_OUT_GUY_IN_CHERRYGROVE)) {
		msgbox(format("Man, lately my girlfriend can't seem to keep her hands off of me.\pI had to get out of the house for some fresh air.\p...\pWhat's that?\p...\pA long distance girlfriend?\pOh god, not this crazy stalker chick again...\pLook, just tell her I moved to the Hoenn region or something.\pYou can have this for keeping my real location a secret from her."))
		giveitem (ITEM_STEELIXITE)
		setflag (FLAG_RECEIVED_STEELIXITE)
		release
		end
	}
	else {
		msgbox(format("Man, lately my girlfriend can't seem to keep her hands off of me.\pI had to get out of the house for some fresh air."))
		release
		end
	}
}

raw `
OldaleTown_EventScript_TownSign::
	msgbox OldaleTown_Text_TownSign, MSGBOX_SIGN
	end

OldaleTown_EventScript_MartEmployee::
	lock
	faceplayer
	goto_if_set FLAG_RECEIVED_POTION_OLDALE, OldaleTown_EventScript_ExplainPotion
	msgbox OldaleTown_Text_IWorkAtPokemonMart, MSGBOX_DEFAULT
	giveitem ITEM_POTION
	goto_if_eq VAR_RESULT, FALSE, OldaleTown_EventScript_BagIsFull
	msgbox OldaleTown_Text_PotionExplanation, MSGBOX_DEFAULT
	setflag FLAG_RECEIVED_POTION_OLDALE
	release
	end

OldaleTown_EventScript_ExplainPotion::
	msgbox OldaleTown_Text_PotionExplanation, MSGBOX_DEFAULT
	release
	end

OldaleTown_EventScript_BagIsFull::
	msgbox gText_TooBadBagIsFull, MSGBOX_DEFAULT
	fadedefaultbgm
	release
	end

OldaleTown_Text_IWorkAtPokemonMart:
	.string "Hi!\n"
	.string "I work at this POKéMON MART.\p"
	.string "We sell a variety of goods including\n"
	.string "POKé BALLS for catching POKéMON, as\l"
	.string "well as POTIONs for healing POKéMON.\p"
	.string "Here, I'd like you to have this as\n"
	.string "a free sample.$"

OldaleTown_Text_PotionExplanation:
	.string "A POTION can be used anytime, so it's\n"
	.string "even more useful than a POKéMON CENTER\l"
	.string "in certain situations.$"

OldaleTown_Text_TownSign:
	.string "CHERRYGROVE CITY\n"
	.string "“The City of Fragrant Flowers.”$"
`